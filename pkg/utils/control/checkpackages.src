/* $Id: checkpackages.src 375 2006-06-17 19:26:32Z austinheilman $
 *
 */
use uo;
use os;
use cfgfile;

include "include/string";
include ":datafile:include/datafile";
include ":control:report";

// Run 255 lines before going to the next script.
// Its as close as it gets to critical without the console whining about it.
Set_Priority(255);
Set_Script_Option(SCRIPTOPT_NO_RUNAWAY, 1);

program CheckPackages()
	ICP_ReportText("---=[ ICP SETUP HAS STARTED UP ]=---", ICP_REPORT_SYSLOG);
	
	var pkg_info := DFOpenDataFile(":control:packages", DF_CREATE);
	var pkg_gumps := DFOpenDataFile(":control:gumps", DF_CREATE);

	// This foreach loop is here until DeleteDataFile() in datafile.em works.
	// Its purpose is to wipe the datafile so its updated for the new package settings.
	DFPurgeFile(pkg_info);
	pkg_gumps.DeleteElement("index");
	
	DF_DEBUG_MODE := 0;

	// Run through each package -
	// If it has an icp.cfg in its 'config' folder, get the settings we want from it.
	// If not, it'll go on the gump as packagename [Not ICP compliant]
	foreach package in (POLCore().packages)
		var temp_info := struct;

		var data_elem := DFFindElement(pkg_info, package, DF_CREATE);

		var icp_cfg := ReadConfigFile(":"+package+":config/icp");
		icp_cfg := icp_cfg["Register"];

		if ( icp_cfg )
			temp_info.+Name := GetCfgValue(icp_cfg, "Name");
			temp_info.+Version := CDbl(GetCfgValue(icp_cfg, "Version"));
			temp_info.+Creator := GetCfgValue(icp_cfg, "Creator");
			temp_info.+C_Email := GetCfgValue(icp_cfg, "C_Email");
			temp_info.+Maintainer := GetCfgValue(icp_cfg, "Maintainer");
			temp_info.+M_Email := GetCfgValue(icp_cfg, "M_Email");
			
			var description := "";
			foreach line in  GetConfigStringArray(icp_cfg, "Description")
				description := description + line+" ";
				SleepMS(2);
			endforeach
			temp_info.+Description := description;
			
			// Makes a list of scripts and text commands that will be on the gump.
			var script_list := GetConfigStringArray(icp_cfg, "Script");
			var cmd_list := GetConfigStringArray(icp_cfg, "TextCmd");

			data_elem.SetProp("Main", temp_info);
			data_elem.SetProp("Scripts", script_list);
			data_elem.SetProp("TextCommands", cmd_list);
		else
			ICP_ReportText("Warning: Package ["+package+"] has no ICP support!", ICP_REPORT_SYSLOG);
			temp_info.+Name := package;
			temp_info.+NoSupport := 1;
			data_elem.SetProp("Main", temp_info);
		endif

		UnloadConfigFile(":"+package+":config/icp");
	endforeach

	ICP_ReportText("ICP setup has completed.", ICP_REPORT_SYSLOG);
	
	return 1;
endprogram


/*
 * GetCfgValue(byref cfg_elem, value)
 *
 * Used to get values from the config file.
 * Makes sure that the value returned is "clean"
 * so <uninitialized object> is not seen on the gump.
 */
function GetCfgValue(byref cfg_elem, value)
	value := GetConfigString(cfg_elem, value);
	if ( !value )
		value := "";
	endif
	return value;
endfunction
