use uo;
use os;
use util;

include ":attributes:attributes";
include ":magery:spells";
include "include/client";
include "include/sounds";
include ":sorcery:sorceryOpts";
include ":damage:damage";

var npccfg := ReadConfigFile("npcdesc");

program flametouch(parms)

    var caster := parms[1];
    var info := parms[2];
    var cast_on;
    cast_on := MS_Target(caster, info.targ, "Select a target.", TGTOPT_CHECK_LOS+TGTOPT_HELPFUL);
    if (!cast_on) // If no cast_on then get cast_on from parms[4]. Used mostly for NPC casting.
        cast_on := parms[4];
    endif
    if (!cast_on)
        SendSysMessage (caster, "Cancelled.", color := 33);
        return 0;
    endif
    if ( !MS_MobileCheck(caster, cast_on) )
        return 0;
    endif

    if ( Distance(cast_on, caster) > 1 )
		SendSysMessage(caster, "Aborted.");
		return 0;
    endif

    AwardRawAttributePoints( caster, MAGERY, 50, SKILLID_MAGERY);

    var dmg := CInt( RandomInt(3) + (AP_GetStat(caster, INTELLIGENCE)/19) );
    PlaySoundEffect(cast_on, 551);
    PlayStationaryEffect(cast_on.x, cast_on.y, cast_on.z, 0x36cb, 7, 0x10, 0, caster.realm);

    if(cast_on.isA(POLCLASS_NPC))
		var element := npccfg[cast_on.npctemplate].elemental;
		if(element == "fire")
			HealDamage(cast_on, dmg);
		elseif(element == "water")
			ApplySpellDamageEX(cast_on, dmg*3, DMG_FIRE, caster, info.Circle, "Flame Touch");
		else
			ApplySpellDamageEX(cast_on, dmg*2, DMG_FIRE, caster, info.Circle, "Flame Touch");
		endif
    else
		ApplySpellDamageEX(cast_on, dmg, DMG_FIRE, caster, info.Circle, "Flame Touch");
    endif

    return 1;

endprogram