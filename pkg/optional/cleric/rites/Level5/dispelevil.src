use uo;
use vitals;

include ":cleric:cleric";

program dispelevil(params)
	var who := params[1];
	var info := params[2];

	var devotion := GetObjProperty(who, "Devotion");
	var piety := DevotionPiety(devotion, who);
	var pietyboost := CInt(GetConcentratingClerics(who, 5)/2);
	if (pietyboost + piety <= 7)
		piety := piety + pietyboost;
	endif

	foreach mobile in ListMobilesNearLocation(who.x, who.y, who.z, (piety+3)*2, who.realm)
		if (mobile.npctemplate && CheckLineOfSight(who, mobile))
			if (issummoned(mobile) && mobile.murderer != who.murderer)
				PlayObjectCenteredEffect(mobile, 0x374a, 20, 20);
				PrintTextAbove(mobile, "*begins to wither and die*");
				mobile.Kill();
			endif
		endif
	endforeach
endprogram

function issummoned(mobile)
	if (GetObjProperty(mobile, "summoned"))
		case(mobile.graphic)
			0x9: return 1; 	// Daemon
			0xA: return 1;	// Daemon
			0xD: return 1;	// Air Elemental
			0xE: return 1;	// Earth Elemental
			0xF: return 1;	// Fire Elemental
			0x10: return 1;	// Water Elemental
		endcase
	endif
endfunction
