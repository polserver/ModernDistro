use os;
use uo;
use util;
use cliloc;

include ":cleric:cleric";
include ":damage:damage";

program retribution(params)
	var who := params[1];
	var info := params[2];

	var devotion := GetObjProperty(who, "Devotion");
	var piety := DevotionPiety(devotion, who);
	var pietyboost := CInt(GetConcentratingClerics(who, 5)/2);
	if (pietyboost + piety <= 7)
		piety := piety + pietyboost;
	endif

	var targ :=ClericalTarget(who, info.npctarget, "Select a target", TGTOPT_CHECK_LOS + TGTOPT_HARMFUL);

	if (!targ.ip && !targ.npctemplate)
		SendSysMessage(who, 1042023, color := 33); // Cancelled.
		return;
	elseif (!DistanceCheck(who, targ, piety+3))
		SendSysMessage(who, 500643, color := 33); // Target is too far away.
		return;
	endif

	piety := piety+2;

	var thepain := RandomDiceRoll("1d10")+50; // 50-60 damage

	PlayMovingEffect(who, targ, 14036,10,30);
	PlaySoundEffect(targ,552);
	ApplyDamageEx(targ, thepain, DMG_PHYSICAL, who);
endprogram
