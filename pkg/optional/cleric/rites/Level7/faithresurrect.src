use uo;

include ":cleric:cleric";

program ressurection(params)
	var who := params[1];
	var info := params[2];

	var devotion := GetObjProperty(who, "Devotion");
	var piety := DevotionPiety(devotion, who);
	var pietyboost := CInt(GetConcentratingClerics(who, 5)/2);
	if (pietyboost + piety <= 7)
		piety := piety + pietyboost;
	endif

	var targ := ClericalTarget(who, info.npctarget, "Target the ghost or corpse to resurrect.");

	if (targ.objtype == 0x2006) // Clicked a corpse
		var resguy := SystemFindObjectBySerial(GetObjProperty(targ, "serial"));
		if (resguy.dead && Distance(targ, resguy) < 20)
			MoveObjectToLocation(resguy, targ.x, targ.y, targ.z, targ.realm);
			res(who, resguy, targ);
		else
			SendSysMessage(who, "Alas, this corpse is too far gone.");
		endif
	elseif (targ.dead)
		res(who, targ);
	else
		SendSysMessage(who, "That is not dead.");
	endif
endprogram

function res(who, player, corpse := 0)
	who := who;
	if (corpse)
		equipt(player, corpse);
	endif
	Resurrect(player);

	PlayObjectCenteredEffect(player, 0x373a, 10, 10);
	PlaySoundEffect(player, 533);
endfunction

function equipt(player, corpse)
	foreach item in EnumerateItemsInContainer(corpse.serial)
		if (item.container == corpse)
			MoveItemToContainer(item, player.backpack.serial);
		endif
	endforeach
	DestroyItem(corpse);
endfunction
