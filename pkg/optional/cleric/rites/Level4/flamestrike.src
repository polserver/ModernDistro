use uo;
use os;
use util;
use cliloc;

include ":cleric:cleric";

program flamestrike(params)
	var who := params[1];
	var info := params[2];

	var devotion := GetObjProperty(who, "Devotion");
	var piety := DevotionPiety(devotion, who);
	var pietyboost := CInt(GetConcentratingClerics(who, 5)/2);
	if (pietyboost + piety <= 7)
		piety := piety + pietyboost;
	endif

	var targ :=ClericalTarget(who, info.npctemplate, "Select a target", TGTOPT_CHECK_LOS + TGTOPT_HARMFUL);
	if (!targ.ip && !targ.npctemplate)
		SendSysMessageCL(who, 1042023, color := 33); // Cancelled.
		return;
	elseif (!DistanceCheck(who, targ, piety+3))
		SendSysMessageCL(who, 500643, color := 33); // Target is too far away.
		return;
	endif

	var i;
	for (i:=1;i<=2;i:=i+1)
		PlaySoundEffect(who, 0x228);
		PlayMovingEffect(who, targ, 0x36D4, 10, 30);
		sleepms(400);
	endfor

	var thepain := RandomDiceRoll("1d10")+40; // 40-50 damage
	ApplyDamage(targ, thepain);
endprogram
