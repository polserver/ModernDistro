use uo;
use os;
use util;
use cliloc;

include ":gumps:yesNo";
include ":cleric:cleric";

program malediction(params)
	var who := params[1];
	var info := params[2];

	var devotion := GetObjProperty(who, "Devotion");
	var piety := DevotionPiety(devotion, who);
	var pietyboost := CInt(GetConcentratingClerics(who, 5)/2);
	if (pietyboost + piety <= 7)
		piety := piety + pietyboost;
	endif

	var targ := ClericalTarget(who, info.npctarget, "Select the one who has mocked your god!", TGTOPT_CHECK_LOS + TGTOPT_HELPFUL);

	if (!targ.ip && ! targ.npctemplate)
		SendSysMessage(who, 1042023, color := 33); // Cancelled.
		return;
	elseif (!yesno(who, "Only use this as a last resort! Are you certain?"))
		SendSysMessage(who, 1042023, color := 33); // Cancelled.
		return;
	endif

	PrintTextAbove(targ, targ.name + " has been cursed by the gods!");
	PlayObjectCenteredEffect(targ, 0x374a, 20, 20);
	PlaySoundEffect(targ, 482);

	var dice := RandomDiceRoll("1d7");

	if (dice >= 1 && dice <= 3)
		// Stat Loss
		StatLoss(targ);
	elseif (dice >=2 && dice <= 7)
		// Equipment tosser
		equipmentdrop(targ);
	endif

endprogram

function StatLoss(mobile)
	var dice := RandomDiceRoll("1d3");
	if (dice == 1)
		PrintTextAbove(mobile, "*" + mobile.name + " feels weaker*");
		SendSysMessage(mobile, "You permanently lost 2 strength");
		AP_SetTrueStat(MOBILE, STRENGTH, AP_GetStat(mobile, STRENGTH) - 2);
	elseif (dice == 2)
		PrintTextAbove(mobile, "*" + mobile.name + " feels less nimble*");
		SendSysMessage(mobile, "You permanently lost 2 dexterity");
		AP_SetTrueStat(MOBILE, DEXTERITY, AP_GetStat(mobile, DEXTERITY) - 2);
	elseif (dice == 3)
		PrintTextAbove(mobile, "*" + mobile.name + " feels less intelligent*");
		SendSysMessage(mobile, "You permanently lost 2 intelligence");
		AP_SetTrueStat(MOBILE, INTELLIGENCE, AP_GetStat(mobile, INTELLIGENCE) - 2);
	endif
endfunction

function EquipmentDrop(mobile)
	foreach item in ListEquippedItems(mobile);
		if (item != mobile.backpack)
			MoveItem(mobile, item);
		elseif (item == mobile.backpack)
			foreach thing in item // Backpack
				if (thing.container == item)
					MoveItem(mobile, thing);
				endif
			endforeach
		endif
	endforeach
	PrintTextAbove(mobile, "*" + mobile.name + " feels colder*");
endfunction

function MoveItem(mobile, item)
	var newx, newy;
	DO
		newx := RandomInt(10)-5;
		newy := RandomInt(10)-5;

		newx := newx+mobile.x;
		newy := newy+mobile.y;
	dowhile(!CheckLOSAt(mobile, newx, newy, GetWorldHeight(newx, newy, mobile.realm)));
	MoveObjectToLocation(item, newx, newy, GetWorldHeight(newx, newy, mobile.realm), mobile.realm, MOVEOBJECT_FORCELOCATION);
endfunction
