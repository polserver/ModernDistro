// squelches speech for a limited duration
use uo;
use os;
use cliloc;

include ":cleric:cleric";

program silence15(params)
	var who := params[1];
	var info := params[2];

	var devotion := GetObjProperty(who, "Devotion");
	var piety := DevotionPiety(devotion, who);
	var pietyboost := CInt(GetConcentratingClerics(who, 5)/2);

	if (pietyboost + piety <= 7)
		piety := piety + pietyboost;
	endif

	var targ := ClericalTarget(who, info.npctarget);

	if (!targ)
		SendSysMessageCL(who, 1042023, color := 33); // Cancelled.
		return;
	elseif (Distance(who, targ) > piety+3)
		SendSysMessageCL(who, 500643, color := 33); // Target is too far away.
		return;
	endif

	ClericReleaseScript(info.pid);

	var i, range := 2;
	var duration := piety * 40;

	for (i:=0; i < duration; i := i+1)

		if (!targ || targ.dead)
			break;
		endif

		var center := targ;
		while (center.container)
			center := center.container;
		endwhile

		SparkleEdge(center, range);
		foreach mobile in ListMobilesNearLocationEx(center.x, center.y, center.z, range, LISTEX_FLAG_NORMAL + LISTEX_FLAG_HIDDEN + LISTEX_FLAG_GHOST, center.realm)
			mobile.squelch(2);
		endforeach
		sleepms(800);

	endfor

endprogram

function SparkleEdge(targ, range)
	var row, col;
	for (row:=targ.x-range; row<=targ.x+range; row:=row+1)
		for(col:=targ.y-range; col<=targ.y+range; col:=col+1)
			if (row == targ.x-range || row == targ.x+range || col == targ.y-range || col == targ.y+range)
				PlayStationaryEffect(row, col, targ.z, 4435, 10, 5, 0, targ.realm);
			endif
		endfor
	endfor
endfunction
