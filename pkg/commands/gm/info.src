/*
 * $Id: info.src 375 2006-06-17 19:26:32Z austinheilman $
 *
 * Purpose
 *
 * Presents information about a mobile.
 *
 */

use uo;
use os;
use storage;

include ":attributes:attributes";
include "include/BAInpcUtil";
include ":gumps:gumps";
include ":gumps:gumpPrompt";
include ":gumps:requestGump";
include ":gumps:yesNo";
include "include/storageAreas";
include "include/weaponInfo";
include "include/arrays";

CONST START_Y	:= 55;   // These are to track the display area height.
CONST END_Y	:= 410;  // These are to track the display area height.
CONST MENU_BTN	:= 0xF000; // Value of the first menu button.

// Storing these globally just makes the script easier to use...
var info_cfg;			// info.cfg
var index_list; 		// info.cfg "Index" lines from Settings elem
var template;			// Template gump (so it doesn't need to be rebuilt)

var btn_list := array{};	// Stores button information for the current gump (since its so dynamic)
var btn_info := struct{"editFunc", "name"}; // Individual button tracking that goes into the btn_list array.

program textcmd_Info(who, serial)
	if ( Lower(TypeOf(who)) == "array" )
		serial := who[2];
		who := who[1];
	endif

	info_cfg := ReadConfigFile(":commands:info");
	if ( !info_cfg )
		SendSysMessage(who, "Error reading in info.cfg ->"+info_cfg);
		return 0;
	else
		index_list := GetConfigStringArray(info_cfg["Index"], "Index");
	endif

	var targ;
	if ( serial )
		targ := SystemFindObjectBySerial(CInt(serial));
	else
		targ := Target(who);
	endif
	if ( targ.IsA(POLCLASS_ITEM) )
		start_script(":commands:/gm/iteminfo", {who, targ.serial});
		return 0;
	endif

	if ( !targ.IsA(POLCLASS_MOBILE) )
		SendSysMessage(who, "Cancelled.");
		return 0;
	endif

	template := GumpTemplate(targ);
	var cur_gump := 1;

	while ( 1 )
		var gump := BuildGump(targ, cur_gump);
		var input := GFSendGump(who, gump);
		input := input[0];

		if ( input >= MENU_BTN )
			cur_gump := input - MENU_BTN;
			btn_list.Shrink(0); // New panel, reset button tracking.
		elseif ( input > 0 )
			ProcessInput(who, targ, input);
		endif

		if ( !input )
			break;
		elseif ( !who.connected )
			break;
		elseif ( !cur_gump )
			break;
		endif
	endwhile
endprogram

function BuildGump(targ, index_num)
	var index := index_list[index_num];
	var gump := template;
	var cfg_elem := info_cfg["Index-"+index];
	var groups := GetConfigStringArray(cfg_elem, "Group");
	var y_pos := START_Y;
	BuildEntries(targ, gump, cfg_elem, y_pos);

	foreach group in ( groups )
		var group_elem := info_cfg[group];
		BuildEntries(targ, gump, group_elem, y_pos);
		SleepMS(2);
	endforeach

	return gump;
endfunction

function BuildEntries(targ, byref gump, byref cfg_elem, byref y_pos)
	var entries := GetConfigStringArray(cfg_elem, "Entry");
	if ( entries.Size() < 1 )
		return 0;
	endif

	if ( cfg_elem.AutoBox )
		var num_entries := entries.Size();
		GFResizePic(gump, 145, y_pos, GetCfgConst("Defaults", "ForeGround"), 340, (num_entries*20));
	endif

	foreach entry in ( entries )
		entry := SplitWords(entry, " ");
		case ( Lower(entry[1]) )
			"member": "custom":
				DisplayMember(targ, gump, entry, y_pos);
				break;
			"vitals":
				DisplayVitals(targ, gump, y_pos);
				break;
			"attributes":
				DisplayAttributes(targ, gump, entry, y_pos);
				break;
			"privileges":
				DisplayPrivileges(targ, gump, y_pos);
				break;
			"text":
				var text := "";
				entry.Erase(1);
				foreach line in ( entry )
					text := text+" "+line;
					SleepMS(2);
				endforeach
				GFTextLine(gump, 175, y_pos, 2100, text);
				y_pos := y_pos + 20;
				break;
			"storageareas":
				DisplayStorageAreas(targ, gump, y_pos);
				break;
			"blank":
				var buffer := CInt(entry[2]);
				if ( !buffer )
					buffer := 20;
				endif
				y_pos := y_pos+buffer;
				break;
			"reportables":
				DisplayReportables(targ, gump, y_pos);
				break;
			default:
				GFTextLine(gump, 175, y_pos, 32, entry);
				y_pos := y_pos +  20;
				break;
		endcase
		SleepMS(2);
	endforeach
	y_pos := y_pos+5;

	return 1;
endfunction

function DisplayMember(targ, byref gump, byref entry, byref y_pos)
	if ( entry[5] )
		var btn_id := GFAddButton(gump, 150, (y_pos+3), 2117, 2118, GF_CLOSE_BTN);
		RegisterButton(btn_id, entry[3], entry[5]);
	endif

	GFTextLine(gump, 175, y_pos, 2100, ParseEntryName(entry[2]));

	var value := "";
	if ( Lower(entry[1]) == "member" )
		value := ParseMembers(targ, entry[3]);
	else
		value := GetCustom(targ, entry[2]);
	endif
	if ( value == error )
		value := "None";
	endif

	case ( Lower(entry[4]) )
		"hex":
			value := Hex(value);
			break;
		"binary":
			value := Bin(CInt(value));
			break;
		"script":
			value := ParseScriptName(value);
			break;
	endcase
	GFTextRight(gump, 480, y_pos, 2100, value);

	y_pos := y_pos +  20;
	return 1;
endfunction

function DisplayVitals(targ, byref gump, byref y_pos)
	var vitals := AP_GetVitalNames();
	var num_vitals := vitals.Size();

	GFResizePic(gump, 145, y_pos, GetCfgConst("Defaults", "ForeGround"), 340, (num_vitals*20));
	foreach vital_name in ( vitals )
		var btn_id := GFAddButton(gump, 150, (y_pos+3), 2117, 2118, GF_CLOSE_BTN);
		RegisterButton(btn_id, vital_name, "vitalEdit");
		GFTextLine(gump, 175, y_pos, 2100, vital_name);
		var value := CDbl(GetVital(targ, vital_name)) / 100;
		var max_value := CDbl(GetVitalMaximumValue(targ, vital_name)) / 100;
		GFTextRight(gump, 480, y_pos, 2100, value+" / "+max_value);
		y_pos := y_pos +  20;
		SleepMS(2);
	endforeach

	return 1;
endfunction

function DisplayAttributes(targ, byref gump, byref entry, byref y_pos)
	var type := entry[2];
//	var attributes := AP_ListAttributesByType(type);
        var attrib_cfg := ReadConfigFile(":*:attributes"); 
        var attributes := GetConfigStringKeys(attrib_cfg); 
	//
	// Notes about the first page of showing attributes:
	// 1. The Y position they start showing on is dynamic.
	// 2. They may all fit on the first page.
	// 3. The remaining attributes on other pages start on START_Y
	// With this in mind, the array must be broken once for the first page.
	// Then broken again for the remaining pages. Not the most 'efficient' way I suppose.
	//

	var y_space := (END_Y-y_pos)+START_Y;
	var available_lines := CInt(y_space / 20);
	var num_attribs := attributes.Size();
	if ( available_lines > num_attribs )
		attributes := array{attributes};
	else
		var page_1 := GetArrayRange(attributes, 1, available_lines);
		var rest := GetArrayRange(attributes, available_lines+1, num_attribs);
		attributes := BreakArray(rest, 20);
		attributes.Insert(1, page_1);
	endif

	var num_groups := attributes.Size();
	foreach group in ( attributes )
		num_attribs := group.Size();

		var y_stretch := num_attribs * 20;
		if ( _group_iter < num_groups )
			y_stretch := y_stretch + 10;
		endif

		GFResizePic(gump, 145, y_pos, GetCfgConst("Defaults", "ForeGround"), 340, y_stretch);
		if ( _group_iter > 1 )
			GFAddButton(gump, 460, START_Y+5, 2650, 2651, GF_PAGE_BTN, gump.cur_page-1);
		endif

		foreach attrib_name in ( group )
			var btn_id := GFAddButton(gump, 150, (y_pos+3), 2117, 2118, GF_CLOSE_BTN);
			RegisterButton(btn_id, attrib_name, "attribEdit");

			GFTextLine(gump, 175, y_pos, 2100, attrib_name);
                        var value := CDbl(GetAttributeBaseValue(targ, attrib_name)) / 10.0;
                        var mod_value := CDbl(GetAttributeTemporaryMod(targ, attrib_name)) / 10.0;

			GFTextRight(gump, 465, y_pos, 2100, value+" (+"+mod_value+")");
			y_pos := y_pos +  20;
			SleepMS(2);
		endforeach

		if ( _group_iter < num_groups )
			GFAddButton(gump, 460, 430, 2648, 2649, GF_PAGE_BTN, gump.cur_page+1);
			GFPage(gump, gump.cur_page+1);
			y_pos := START_Y;
		endif

		SleepMS(2);
	endforeach

	return 1;
endfunction

function DisplayPrivileges(targ, byref gump, byref y_pos)
	var privs := info_cfg["PrivList"];
	privs := GetConfigStringArray(privs, "Priv");

	var num_privs := privs.Size();
	GFResizePic(gump, 145, y_pos, GetCfgConst("Defaults", "ForeGround"), 340, (num_privs*20));
	foreach priv_name in ( privs )
		var btn_id := GFAddButton(gump, 150, (y_pos+3), 2117, 2118, GF_CLOSE_BTN);
		RegisterButton(btn_id, priv_name, "privEdit");

		GFTextLine(gump, 175, y_pos, 2100, priv_name);

		var status := "Disabled";
		if ( targ.Enabled(priv_name) )
			status := "Enabled";
		endif

		GFTextRight(gump, 480, y_pos, 2100, status);
		y_pos := y_pos +  20;
		SleepMS(2);
	endforeach

	return 1;
endfunction

function DisplayStorageAreas(targ, byref gump, byref y_pos)

	var storage_areas := CP_GetStorageAreaNames();
	var num_areas := storage_areas.Size();

	GFResizePic(gump, 145, y_pos, GetCfgConst("Defaults", "ForeGround"), 340, (num_areas*20));
	foreach area_name in ( storage_areas )
		var container := CP_GetStorageContainerForMobile(targ, area_name, CP_NOCREATE);
		var name := area_name;
		if ( container )
			var btn_id := GFAddButton(gump, 150, (y_pos+3), 2117, 2118, GF_CLOSE_BTN);
			RegisterButton(btn_id, area_name, "openStorage");
		else
			GFTextRight(gump, 480, y_pos, 2100, "(no container)");
		endif

		GFTextLine(gump, 175, y_pos, 2100, name);
		y_pos := y_pos +  20;
		SleepMS(2);
	endforeach

	return 1;
endfunction

function DisplayReportables(targ, byref gump, byref y_pos)
	GFResizePic(gump, 145, y_pos, GetCfgConst("BackGrounds", "GoldTrim"), 340, 200);
	var lines := "<CENTER><U>Reportables</U></CENTER><BR>";
	foreach line in ( targ.reportables )
		lines := "<B>o</B>"+lines+line+"<BR>";
		SleepMS(2);
	endforeach
	GFHTMLArea(gump, 155, y_pos+7, 325, 186, lines, 0, 1);

	y_pos := y_pos+340;

	return 1;
endfunction

function DisplayWeaponInfo(targ, byref gump, byref entry, byref y_pos)
	var weapon := targ.weapon;

	case ( Lower(entry[1]) )
		"weaponspeed":
		"damagedice":
		"averagedamage":
	endcase

	y_pos := y_pos +  20;

	return 1;
endfunction

function ParseEntryName(entry_name)
	while ( entry_name["_"] )
		entry_name["_"] := " ";
		SleepMS(2);
	endwhile

	return entry_name;
endfunction

function ParseMembers(targ, members, new_val:=error)
	members := SplitWords(members, ".");
	var result := targ;
	foreach member in ( members )
		var temp := result.Get_Member(member);

		if ( new_val != error && _member_iter == members.Size() )
			var old_type := Lower(TypeOf(temp));
			if ( new_val == "NULL" )
				new_val := "";
			endif
			case ( old_type )
				"integer"	: new_val := CInt(new_val); break;
				"string"	: new_val := CStr(new_val); break;
				"double"	: new_val := CDbl(new_val); break;
				default:
					return error{"errortext":="'"+old_type+"' is not a supported data type."};
			endcase

			return result.Set_Member(member, new_val);
		elseif ( temp == error )
			return error;
		else
			result := temp;
		endif

		SleepMS(2);
	endforeach

	return result;
endfunction

function GetCustom(targ, property)
	case ( Lower(property) )
		"weapon_speed":
			return CS_GetWeaponInfo(targ.weapon, "Speed");
		"damage_dice":
			return CS_GetWeaponInfo(targ.weapon, "Damage");
		"average_damage":
			return CS_GetAverageDamage(targ.weapon);
		"gotten_item_name":
			return targ.GetGottenItem().desc;
		"gotten_item_serial":
			return targ.GetGottenItem().serial;
	endcase

	return "Unknown custom type!";
endfunction

function ParseScriptName(path)
	while ( path["/"] )
		// Breaks it down to just the file name.
		var pos := Find(path, "/", 1);
		path[1, pos] := "";
		SleepMS(2);
	endwhile

	return path;
endfunction

function GumpTemplate(targ)
	var gump := GFCreateGump();
	GFPage(gump, 0);
	GFResizePic(gump, 0, 0, GetCfgConst("Defaults", "BackGround"), 500, 480);

	GFResizePic(gump, 15, 15, GetCfgConst("Defaults", "ForeGround"), 470, 35);
	GFTextLine(gump, 25, 22, 2100, "Info on "+targ.name);

	if ( targ.npctemplate )
		GFTextRight(gump, 475, 22, 2100, "[NPC]");
	elseif ( targ.connected )
		GFTextRight(gump, 475, 22, 2100, "[Player]");
	else
		GFTextRight(gump, 475, 22, 2100, "[Offline Player]");
	endif

	GFResizePic(gump, 15, START_Y, GetCfgConst("Defaults", "ForeGround"), 125, END_Y);

	var y_pos := 60;
	foreach entry in ( index_list )
		GFAddButton(gump, 20, (y_pos+3), 2117, 2118, GF_CLOSE_BTN, (MENU_BTN+_entry_iter));
		GFTextLine(gump, 40, y_pos, 2100, entry);
		y_pos := y_pos +  20;
		SleepMS(2);
	endforeach
	GFPage(gump, 1);

	return gump;
endfunction

function RegisterButton(btn_id, name, edit_function)
	btn_info.editFunc := edit_function;
	btn_info.name := name;
	btn_list[btn_id] := btn_info;

	return 1;
endfunction

function ProcessInput(who, targ, input)
	var input_info := btn_list[input];

	case ( Lower(CStr(input_info.editFunc)) )
		"propedit":
			PropEdit(who, targ, input_info);
			break;
		"coordedit":
			CoordEdit(who, targ, input_info);
			break;
		"disconnect":
			DisconnectClient(targ);
			GumpPrompt(who, "Client has been disconnected.");
			break;
		"methodedit":
			MethodEdit(who, targ, input_info);
			break;
		"privedit":
			PrivEdit(who, targ, input_info);
			break;
		"openstorage":
			OpenStorage(who, targ, input_info);
			break;
		"vitaledit":
			VitalEdit(who, targ, input_info);
		"attribedit":
			AttributeEdit(who, targ, input_info);
		default:
			GumpPrompt(who, "Dont know what to do with edit function type '"+input_info.editFunc+"'");
			break;
	endcase

	return 0;
endfunction

function PropEdit(who, targ, input_info)
	var value := ParseMembers(targ, input_info.name);

	var change := RequestGump(who, "New value to assign "+input_info.name+" to.", "'NULL' to set as non-value.", CStr(value), CANCEL_BTN_ON);
	if ( change != error )
		ParseMembers(targ, input_info.name, change);
	endif

	return 1;
endfunction

function CoordEdit(who, targ, input_info)
	var value := ParseMembers(targ, input_info.name);

	var change := RequestGump(who, "New value to assign "+input_info.name+" to.", "", CStr(value), CANCEL_BTN_ON);
	if ( change == error )
		return 0;
	endif

	var x := targ.x;
	var y := targ.y;
	var z := targ.z;
	var realm := targ.realm;

	case ( Lower(input_info.name) )
		"x":
			x := CInt(change);
			break;
		"y":
			y := CInt(change);
			break;
		"z":
			z := CInt(change);
			break;
		"realm" :
			realm := CStr(change);
			break;
	endcase

	var result := MoveObjectToLocation(targ, x, y, z, realm, MOVEOBJECT_FORCELOCATION);
	if ( result.errortext )
		GumpPrompt(who, "Movement Error [P] "+result);
	endif

	return 1;
endfunction

function MethodEdit(who, targ, input_info)
	var value := ParseMembers(targ, input_info.name);

	var change := RequestGump(who, "New value to assign "+input_info.name+" to.", "", CStr(value), CANCEL_BTN_ON);
	if ( change == error )
		return 0;
	endif

	case ( Lower(input_info.name) )
		"criminal":
			targ.SetCriminal(CInt(change));
			break;
		"murderer":
			targ.SetMurderer(CInt(change));
			break;
		"warmode":
			targ.SetWarMode(CInt(change));
			break;
		"squelched":
			targ.Squelch(CInt(change));
			break;
		default:
			GumpPrompt(who, "Dont know how to handle method '"+input_info.name+"'");
			break;
	endcase

	return 1;
endfunction

function VitalEdit(who, targ, input_info)

	var value := GetVital(targ, input_info.name) / 100;
	var change := RequestGump(who, "New value to assign "+input_info.name+" to.", "", CStr(value), CANCEL_BTN_ON);
	if ( change == error )
		return 0;
	endif

	SetVital(targ, input_info.name, CInt(CDbl(change)*100.0));

	return 1;
endfunction

function AttributeEdit(who, targ, input_info)

	var real_value := CDbl(GetAttributeBaseValue(targ, input_info.name)) / 10.0;
	var mod_value := CDbl(GetAttributeTemporaryMod(targ, input_info.name)) / 10.0;
	var value := real_value+" "+mod_value;
	var change := RequestGump(who, "New value to assign "+input_info.name+" to.", "real_value("+real_value+") modifier("+mod_value+")", CStr(value), CANCEL_BTN_ON);
	if ( change == error )
		return 0;
	endif

	change := SplitWords(change, " ");
	SetAttributeBaseValue(targ, input_info.name, CInt(CDbl(change[1]) * 10.0));
	SetAttributeTemporaryMod(targ, input_info.name, CInt(CDbl(change[2]) * 10.0));
        RecalcVitals(targ);
	return 1;
endfunction

function PrivEdit(who, targ, input_info)
	who := who; // Stops compiler warning.
	var priv_name := input_info.name;

	if ( targ.Enabled(priv_name) )
		targ.Disable(priv_name);
		RevokePrivilege(targ, priv_name);
	else
		GrantPrivilege(targ, priv_name);
		targ.Enable(priv_name);
	endif

	return 1;
endfunction

function OpenStorage(who, targ, input_info)
	var storage_area := input_info.name;

	// Kind of a kludge, but oh well, deal with it!
	if ( storage_area == "backpack" )
		SendOpenSpecialContainer(who, targ.backpack);
		return 1;
	else
		var container := CP_GetStorageContainerForMobile(targ, storage_area, CP_NOCREATE);
		return SendOpenSpecialContainer(who, container);
	endif
endfunction
