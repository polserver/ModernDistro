// $Id: greenPotion.src 1059 2007-05-11 15:34:30Z tekproxy $

use cfgfile;
use cliloc;

include ":alchemy:drinkPotion";
include ":timedScripts:timedScripts";
include ":itemUtils:itemdesc";

program use_greenpotion(who, potion)
	if( (!potion.movable) || !ReserveItem(potion) )
		SendSysMessageCL(who, 501973, {}, 3, 33); // You cannot use that.
		return 0;
	elseif( !(potion in EnumerateItemsInContainer(who.backpack)) )
		SendSysMessageCL(who, 1042593, {}, 3, 33); // That is not in your backpack.
		return 0;
	endif

	EraseObjProperty(who, "#IsMeditating");
	EraseObjProperty(who, "#HealTimer");

	if ( !CheckToDrink(who, "#green_potion") )
		return 0;
	endif

	do_poison(who, potion);
endprogram

function do_poison(who, potion)
	var item_config := ReadConfigFile(":alchemy:itemdesc");
	if(item_config.errortext)
		Print(item_config.errortext);
	endif
	var config_elem := FindConfigElem(item_config, potion.objtype);
	if(config_elem.errortext)
		Print(config_elem.errortext);
	endif
	var strength := CInt(GetConfigInt(config_elem, "strength"));
	if(strength.errortext)
		Print(strength.errortext);
	endif

	empty_bottle(who, potion);
	PrintTextAbovePrivateCL(who, 1010512, {}, 3, 33, who); // You have been poisoned!
	/*
	if ( who.cmdlevel > 1 )
		return 0;
	endif
	*/
	TS_StartTimer(who, "DefaultPoison", 120, strength);
endfunction