use cliloc;

include ":alchemy:drinkPotion";
include ":timedScripts:timedScripts";
include ":itemUtils:itemdesc";

program use_bluepotion(who, potion)

	if( (!potion.movable) || !ReserveItem(potion) )
		SendSysMessageCL(who, 501973, {}, 3, 33); // You cannot use that.
		return 0;
	elseif( !(potion in EnumerateItemsInContainer(who.backpack)) )
		SendSysMessageCL(who, 1042593, {}, 3, 33); // That is not in your backpack.
		return 0;
	endif
	var potion_info := GetItemDescInfo(GetObjtype( potion ));


	EraseObjProperty(who, "#IsMeditating");
	EraseObjProperty(who, "#HealTimer");

	// Prevents mod from being applied twice.
	// Uses the same as Magery Agility, to keep them from stacking.
	if ( GetObjProperty(who, "AgilityMod") )
		SendSysMessage(who, "You are already under the influence.");
		return 0;
	endif

	if( !CheckToDrink(who, "#blue") )
		return 0;
	endif
	case(Lower(potion_info.Name))
		"agilitypotion":  empty_bottle(who, potion);
			 TS_StartTimer(who, "agilitypotion", 60, 10, who);
		"greateragility":  empty_bottle(who, potion);
			 TS_StartTimer(who, "agilitypotion", 180, 20, who);
	endcase

	return 1;

endprogram

