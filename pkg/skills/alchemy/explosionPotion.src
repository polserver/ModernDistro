use uo;
use os;

program use_explosionpotion(who, potion)

	if ( potion.stackable && potion.amount > 1 )
		SendSysMessage(who, "You cannot throw a stack of those, are you crazy?!");
		return 0;
	endif
	if( (!potion.movable) || !ReserveItem(potion) )
		SendSysMessage(who, "You cannot use that");
		return 0;
	endif

	EraseObjProperty(who, "#IsMeditating");
	EraseObjProperty(who, "#HealTimer");

	who.hidden := 0;

	if( !GetObjProperty(potion, "#bomb") )
		var parms := {};
		parms[1] := potion;
		parms[2] := who;
		start_script(":alchemy:explosionTimer", parms);
	endif
	SetObjProperty(potion, "#bomb", 1);

	SendSysMessage(who, "Select a target.");
	var throw_to := TargetCoordinates( who );
	if(!throw_to)
		return 0;
	endif

	if(!potion)
		return 0;
	endif

	if(!CheckLosAt(who, throw_to.x, throw_to.y, throw_to.z))
		MoveObjectToLocation(potion, who.x, who.y, who.z, who.realm, MOVEOBJECT_FORCELOCATION );
		SendSysMessage(who, "You can't see that!");
		return 0;
	endif

	if(!potion)
		return 0;
	endif

	Detach();

	PlayMovingEffectXYZ( who.x, who.y, who.z+15, throw_to.x, throw_to.y, throw_to.z, 0x0f0d, 8, 0, 0, who.realm );

	if(!potion)
		return 0;
	endif

	MoveObjectToLocation(potion, throw_to.x, throw_to.y, throw_to.z, who.realm, MOVEOBJECT_FORCELOCATION );
	
endprogram