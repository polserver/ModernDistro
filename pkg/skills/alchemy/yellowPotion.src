use cliloc;

include ":alchemy:drinkPotion";
include ":timedScripts:timedScripts";
include ":itemUtils:itemdesc";

program use_bluepotion(who, potion)

	if( (!potion.movable) || !ReserveItem(potion) )
		SendSysMessageCL(who, 501973, {}, 3, 33); // You cannot use that.
		return 0;
	elseif( !(potion in EnumerateItemsInContainer(who.backpack)) )
		SendSysMessageCL(who, 1042593, {}, 3, 33); // That is not in your backpack.
		return 0;
	endif
	var potion_info := GetItemDescInfo(GetObjtype( potion ));


	EraseObjProperty(who, "#IsMeditating");
	EraseObjProperty(who, "#HealTimer");

	if( !CheckToDrink(who, "#yellow") )
		return 0;
	endif

	if( TS_GetTimerNames(who, "P").size() > 0 )
		SendSysMessageCL(who, 1156868, {}, 3, 33); // You may not use this while poisoned!
		return 0;
	endif

	case(potion_info.Name)
		"lesserheal": HealDamage(who, (RandomInt(7)  + 3));
			empty_bottle(who, potion);
		"healpotion": HealDamage(who, (RandomInt(14) + 6));
			empty_bottle(who, potion);
		"greaterheal": HealDamage(who, (RandomInt(21) + 9));
			empty_bottle(who, potion);
	endcase
	PlaySoundEffect( who,  SOUND_EFFECT_HEAL );
	PlayObjectCenteredEffect(who, EFFECT_SPARKLE, 10,10);

	return 1;

endprogram