use uo;
use cfgfile;
use util;
use basic;
use os;

include "include/client";
include ":alchemy:potion";
include "include/canAccess";
include "include/noto";

const REAGENT_START := 0x0f78;
const REAGENT_STOP := 0x0f91;
const SOUND_EFFECT_GRIND := 0x243;
const SOUND_EFFECT_POUR := 0x241;
const SOUND_EFFECT_BUBBLE :=  0x4f;
const UOBJECT_EMPTY_BOTTLE := 0x0f0e;

var conf := ReadConfigFile("alchemy");

program use_mortar (who, mortar)
  EraseObjProperty(who, "IsMeditating");
  EraseObjProperty(who, "HealTimer");
  if(!can_access(who, mortar))
    return;
  endif
  if(!ReserveItem(mortar))
    SendSysMessage(who, "That is already in use.");
    return;
  endif
  Detach();
  if(GetObjProperty(mortar, "contents"))
	PourPotion(who, mortar);
  else
    SendSysMessage( who, "What would you like to use that on?" );
    make_the_potion(who, mortar);
  endif
  EraseObjProperty(mortar, "#IsMixing");
endprogram

function ExceptionalFameMod(who, skill, diff, points)
  if(skill > diff - 20)
    diff := diff + 20;
    if((skill - diff) <= 5)
      points := points;
    elseif((skill - diff) <= 10)
      points := (points * 3) / 4;
    elseif((skill - diff) <= 15)
      points := points / 2;
    elseif((skill - diff) <= 20)
      points := points / 4;
    else
      points := 0;
    endif
    points := Cint(points);
    var fame := Cint(GetObjProperty(who, "Fame"));
    var famegain := fame + points;
    SetObjProperty(who, "Fame", famegain);
    var msgtext := "You have ";
    if(points < 0)
      msgtext := msgtext + "lost ";
      points := Abs(points);
    else
      msgtext := msgtext + "gained ";
    endif
    if(points > 150)
      msgtext := msgtext + "a great amount of ";
    elseif(points > 125)
      msgtext := msgtext + "alot of ";
    elseif(points > 75)
      msgtext := msgtext + "a good amount of ";
    elseif(points > 30)
      msgtext := msgtext + "some ";
    elseif(points > 0)
      msgtext := msgtext + "a little ";
    else
      return 0;
    endif
    msgtext := msgtext + "Fame.";
    SendSysMessage(who, msgtext);
    var karma := Cint(GetObjProperty(who, "Karma"));
    var kmod := GetKarmaLevel(karma);
    var fmod := GetFameLevel(famegain);
    var newtitle := nototitles[ (kmod) ];
    newtitle := "The " + CStr(newtitle[fmod]) + " ";
    if(newtitle["None"])
      newtitle := "";
    endif
    if(fmod == 5)
      if(who.gender == 1)
        newtitle := newtitle + "Lady ";
      else
        newtitle := newtitle + "Lord ";
      endif
    endif
    if(newtitle != who.title_prefix)
      who.title_prefix := newtitle;
      SendSysMessage(who, "you are now known as " + newtitle + who.name);
    endif
    SetNotoTitle(who, karma, fame);
  endif
endfunction