// $Id: bowcraft.src 1473 2009-03-12 01:26:41Z muaddib_pol $

use cfgfile;
use util;

include ":attributes:attributes";
include ":crafting:crafting";
include ":crafting:craftingRepair";
include ":crafting:toolWear";

var item_config  := ReadConfigFile(":*:itemdesc");
var craft_config := ReadConfigFile(":bowcraft:bowcraft");

program use_FletchTool(who, tool)

	if ( !ReserveItem(tool) || !tool.movable )
		SendSysMessage(who, "You cannot use that.");
		return 0;
	elseif( !(tool in EnumerateItemsInContainer(who.backpack)) )
		SendSysMessage(who, "That item is not in your backpack.");
		return 0;
	endif

	EraseObjProperty(who, "#IsMeditating");
	EraseObjProperty(who, "#HealTimer");

	var rinfo := sendCraftGump(who, BOWCRAFT_FLETCHING);

	// OK was pressed, close it on out
	if(rinfo == 0)
		return 0;
	endif

	var the_objtype := rinfo[1];
	var amt         := rinfo[2];
	var repairit    := rinfo[3];

	if(repairit)
		repair_this(who, craft_skill);
		return 0;
	endif

	if(craft_config[the_objtype].assemble == 1)
		CraftAssembly(who, BOWCRAFT_FLETCHING, tool, the_objtype, amt);
	else
		makeBowcraftItems(who, the_objtype, amt, tool);
	endif

endprogram

function makeBowcraftItems(who, what, amt, tool)

	if(!what)
		SendSysMessage(who, "Cancelled");
		return 0;
	endif

	// ResourceList entry type.
	var itemchk := MaterialSelection(who, craft_skill, lower(craft_config[what].Material_1));
	if (!itemchk)
		return 0;
	endif

	var use_on := itemchk[1];
	var ptmod := itemchk[3];

	var num, theitem;
	var itemname := craft_config[what].name;
	var material := Cint(craft_config[what].Mat_1_Amt);
	var reqd := Cint(craft_config[what].skill);
	var diff := Cint(craft_config[what].difficulty);
	var pts  := Cint(craft_config[what].points);
	pts      := CInt(pts + ptmod);
	var counter := 0, x := who.x, y := who.y;

	while(counter < amt)
		if((who.x != x) || (who.y != y))
			SendSysMessage(who, "You stop fletching.");
			break;
		endif
		if(!tool)
			break;
		endif

		if(GetAmount(use_on) >= material)
			if(CraftCheckSkill(who, craft_skill, diff, pts))
				theitem := CreateCraftItem(who, craft_skill, use_on, use_on.objtype, what, material, itemname, reqd);
				if( TypeOf(theitem) != "ItemRef" )
					SubtractAmount(use_on, (material / 2));
					SendSysMessage(who, "You failed to create the item, and some of your materials are lost.");
					counter := counter + 1;
					continue;
				endif
				CheckToolWear (who, tool, craft_skill);
			else
				num := CInt(randomint(material)/2) + 1;
				if(SubtractAmount(use_on, num))
					SendSysMessage(who, "You lost some material");
				endif
			endif
		else
			SendSysMessage(who, "You don't have the resources required to make that item.");
			return 0;
		endif
		counter := counter + 1;
	endwhile

	return 0;

endfunction
