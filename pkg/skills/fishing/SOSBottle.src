use uo;
use cfgfile;
use os;
use util;

include "include/client";
include "include/objtype";
include "include/startEqp";
include "include/dist";
include "include/canAccess";

program use_bottle( who, bottle )
  if(!can_access(who, bottle))
    return;
  endif
  EraseObjProperty(who, "IsMeditating");
  EraseObjProperty(who, "HealTimer");
  if(bottle.movable == 0)
    SendSysMessage(who, "You cannot use that.");
    return;
  endif
  if(!ReserveItem(bottle))
    return;
  endif
  var cfg := ReadConfigFile("sosarea");
  if (!cfg)
    SendSysMessage(who, "Could not read sosarea.cfg");
  endif
  var maxi := GetConfigMaxIntKey(cfg);
  var elem := cfg[RandomInt(maxi)+1];
  var X := CInt(elem.x)+RandomInt(CInt(elem.range)*2) - CInt(elem.range);
  var Y := CInt(elem.y)+RandomInt(CInt(elem.range)*2) - CInt(elem.range);
  UnloadConfigFile("sosarea");
  SetObjProperty(bottle, "tileX", X);
  SetObjProperty(bottle, "tileY", Y);
  var message := CreateItemInContainer(who.backpack, 0x1A363);
  if(!message)
    DestroyItem(bottle);
    SendSysMessage(who, "You cannot make out the writing on the message!");
    return;
  endif
  SetObjProperty(message, "tileX", X);
  SetObjProperty(message, "tileY", Y);
  SetObjProperty(message, "msgnum", RandomInt(4));
  SendSysMessage(who, "You have extracted a message from the bottle!");
  DestroyItem(bottle);
endprogram