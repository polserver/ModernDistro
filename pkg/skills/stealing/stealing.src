/* $Id: stealing.src 1053 2007-04-27 22:00:25Z tekproxy $
 *
 *
 * http://uo.stratics.com/content/skills/stealing.shtml
 *
 */

use uo;
use os;
use math;

include ":attributes:attributes";
include "include/facings";

CONST STEAL_NOTIFY_RANGE := 15;
CONST MAX_WEIGHT_FACTOR := 10;
CONST RANDOM_DIFFICULTY_RANGE := 30;

program skill_Stealing(mobile)
	if ( CInt(GetObjProperty(mobile, "#StealWait")) > ReadGameClock() )
		SendSysMessage(mobile, "You must wait to use your stealing skill again.");
		return 0;
	endif

	SendSysMessage(mobile, "Select who to steal from.");
	var targ := Target(mobile);
	if ( !targ.IsA(POLCLASS_MOBILE) )
		SendSysMessage(mobile, "Cancelled.");
		return 0;
	endif

	var difficulty :=  CInt((AP_GetStat(targ, INTELLIGENCE)/2) + (AP_GetStat(targ, DEXTERITY)/2) );
	difficulty := difficulty+RandomInt(RANDOM_DIFFICULTY_RANGE);

	if ( SkillCheck(mobile, "Stealing", difficulty) > 0 )
		var skill_lvl := AP_GetSkill(mobile, STEALING);
		var item_list := FindStealableItems(targ.backpack, skill_lvl);
		var stolen := item_list[RandomInt(item_list.Size())+1];
		MoveItemToContainer(stolen, mobile.backpack);
		SendSysMessage(mobile, "You manage to steal "+stolen.desc);
	else
		SendSysMessage(mobile, "You were unable to steal anything.");
	endif

	NotifyNoticers(mobile, targ);

	return 1;
endprogram

function FindStealableItems(container, skill_lvl)
	var max_weight := CInt(Ceil(skill_lvl / MAX_WEIGHT_FACTOR));

	var item_list := array;
	foreach item in ( EnumerateItemsInContainer(container) )
		if ( item.weight > max_weight )
			// Too heavy
			continue;
		endif
		item_list.Append(item);
	endforeach

	return item_list;
endfunction

function NotifyNoticers(mobile, targ)
	foreach player in ( ListMobilesInLineOfSight(targ, STEAL_NOTIFY_RANGE) )
		if ( !player.connected )
			continue;
		elseif ( IsFacing(player, targ.x, targ.y) )
			SendSysMessage(mobile, "You notice "+mobile.name+" attempting to steal from "+targ.name);
		endif
		SleepMS(2);
	endforeach

	return 1;
endfunction
