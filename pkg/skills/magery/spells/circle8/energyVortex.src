use vitals;
use os;
use uo;
use util;
use cliloc;

include ":attributes:attributes";
include ":magery:spells";
include "include/client";
include ":damage:damage";

program cast_summon( parms )
  var caster := parms[1];
  var info := parms[2];
  var cast_on := MS_TargetCoordinates(caster, info.targ, "Select a target.");
    if (!cast_on) // If no cast_on then get cast_on from parms[4]. Used mostly for NPC casting.
        cast_on := parms[4];
    endif
    if (!cast_on)
        SendSysMessageCL(caster, 1042023, color := 33); // Cancelled.
        return 0;
    endif
  if ( !CheckLosAt(caster, cast_on.x, cast_on.y, cast_on.z) )
        return 0;
  endif

  var thecritter := CreateNpcFromTemplate("vortex", cast_on.x, cast_on.y,cast_on.z, 0, caster.realm);
  PlaySoundEffect(thecritter,SFX_SPELL_ENERGY_VORTEX);
  SetObjProperty(thecritter, "summoned",1);
  SetObjProperty(thecritter, "master", caster.serial);
  Detach();
  sleep( (GetAttribute( caster, MAGERY ) ) );
  PlaySoundEffect( thecritter, SFX_SPELL_MANA_VAMPIRE );
  PlayStationaryEffect( thecritter.x, thecritter.y, thecritter.z, FX_SMOKE, 0xa, 0xa,0 ,thecritter.realm );
  MoveObjectToLocation(thecritter,0,0,0,"britannia",MOVEOBJECT_FORCELOCATION);
  KillMobile(thecritter);
endprogram