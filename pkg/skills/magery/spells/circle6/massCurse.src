use os;
use uo;
use util;
use cliloc;

include ":attributes:attributes";
include ":timedScripts:timedScripts";
include ":magery:spells";

program cast_curse( parms )
  var caster := parms[1];
  var info := parms[2];
  var cast_on := MS_TargetCoordinates(caster, info.targ, "Select a target.");
    if (!cast_on) // If no cast_on then get cast_on from parms[4]. Used mostly for NPC casting.
        cast_on := parms[4];
    endif
    if (!cast_on)
        SendSysMessageCL(caster, 1042023, color := 33); // Cancelled.
        return 0;
    endif
  if ( !CheckLosAt(caster, cast_on.x, cast_on.y, cast_on.z) )
        return 0;
  endif

	MS_PlaySpellSFX(info.spell_id, cast_on);

	// Get all targets in area excluding party and self.
	var area_size := GetConfigInt(info.spell_config, "AreaSize");
	var party_members := caster.party.members;
	var excludes := {};
	if ( party_members == error )
			excludes := array{caster.serial};
	else
		foreach member in party_members
			excludes.Append(member.serial);
		endforeach
	endif

	// Duration: 120% Magery
	var duration := CInt(AP_GetSkill(caster, MAGERY) * 1.2);
	var targs := MS_GetValidTargets(cast_on, area_size, 1, excludes);
	foreach vtarg in targs
		MS_PlaySpellGFX(info.spell_id, vtarg, caster);
		TS_StartTimer(vtarg, "MassCurse", duration, 0, caster);
	endforeach

	return 1;
endprogram
