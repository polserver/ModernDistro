use vitals;
use uo;
use cfgfile;

include ":attributes:attributes";
include ":timedScripts:timedScripts";
include ":timedScripts:poisonUtil";
include ":magery:spells";
include ":virtue:noto";

var npccfg := ReadConfigFile("npcdesc");

program cast_cure( parms )
    var caster := parms[1];
    var info := parms[2];
    var cast_on;
    cast_on := MS_Target(caster, info.targ, "Select a target.", TGTOPT_CHECK_LOS+TGTOPT_HELPFUL);
    if (!cast_on) // If no cast_on then get cast_on from parms[4]. Used mostly for NPC casting.
        cast_on := parms[4];
    endif
    if (!cast_on)
        SendSysMessage (caster, "Cancelled.", color := 33);
        return 0;
    endif
    if ( !MS_MobileCheck(caster, cast_on) )
        return 0;
    endif

	// Get poison level
	var poison_names := TS_GetTimerNames(cast_on, "P");
	var poison_level := 1;

	foreach timer_name in ( poison_names )
		var timer_info := TS_GetTimerInfo(cast_on, timer_name);
		if ( timer_info.level > poison_level )
				poison_level := timer_info.level;
		endif
	endforeach

	//% Chance to cure: (Magery * 0.75) + (110 - (Poison level * 33)) .
	var chance := CInt(AP_GetSkill(caster, MAGERY) * 0.75) + (110 - (poison_level * 33));
	if ( chance >= RandomInt(100) + 1 )
		// NOTE: There is still a 1 in 100 chance that this wont work..
		TS_CurePoison(cast_on, 10000, "P", poison_level);
		if(cast_on != caster)
			VS_AdjustVirtue(caster, "compassion", 1, 100);
		endif
		if(cast_on != caster)
			VS_AdjustVirtue(caster, "compassion", 1, 35);
		endif

	endif

	MS_PlaySpellSFX(info.spell_id, cast_on);
	MS_PlaySpellGFX(info.spell_id, cast_on, caster);

	return 1;

endprogram
