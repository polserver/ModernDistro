use uo;
use os;
use util;

include ":attributes:attributes";
include ":timedScripts:timedScripts";
include ":magery:spells";

program cast_bless( parms )
    var caster := parms[1];
    var info := parms[2];
    var cast_on;
    cast_on := MS_Target(caster, info.targ, "Select a target.", TGTOPT_CHECK_LOS+TGTOPT_HELPFUL);
    if (!cast_on) // If no cast_on then get cast_on from parms[4]. Used mostly for NPC casting.
        cast_on := parms[4];
    endif
    if (!cast_on)
        SendSysMessage (caster, "Cancelled.", color := 33);
        return 0;
    endif
    if ( !MS_MobileCheck(caster, cast_on) )
        return 0;
    endif

	MS_PlaySpellSFX(info.spell_id, cast_on);
	MS_PlaySpellGFX(info.spell_id, cast_on, caster);

	// Str increase % = (Eval / 10 + 1)
	var percent := CDbl(AP_GetSkill(caster, EVALUATING_INTELLIGENCE) / 10) + 1;
	var points := CInt(AP_GetSkill(caster, MAGERY) * (percent / 100));

	// Duration: ((Eval / 5) + 1) * 6
	var duration := ((AP_GetSkill(caster, EVALUATING_INTELLIGENCE) / 5) + 1) * 6;

	// Same as casting agility, cunning and strength all at once.
	// Bless should probably not be able to stack with Agility, etc. So we do it this way.
	TS_StartTimer(cast_on, "Bless", duration, points, caster);

	return 1;

endprogram
