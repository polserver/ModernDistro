use uo;
use os;
use util;
use cliloc;

include ":attributes:attributes";
include ":magery:spells";
include "include/client";

program cast_heal( parms )
    var caster := parms[1];
    var info := parms[2];
    var cast_on;
    cast_on := MS_Target(caster, info.targ, "Select a target.", TGTOPT_CHECK_LOS+TGTOPT_HELPFUL);
    if (!cast_on) // If no cast_on then get cast_on from parms[4]. Used mostly for NPC casting.
        cast_on := parms[4];
    endif
    if (!cast_on)
        SendSysMessageCL(caster, 1042023, color := 33); // Cancelled.
        return 0;
    endif
    if ( !MS_MobileCheck(caster, cast_on) )
        return 0;
    endif

	// Amount healed: 40% Magery + (1 to 10)
	var points := CInt(AP_GetSkill(caster, MAGERY) * 0.4) + ( RandomInt(10) + 1 );

	MS_PlaySpellSFX(info.spell_id, cast_on);
	MS_PlaySpellGFX(info.spell_id, cast_on, caster);

	var healed := AP_GetVital(cast_on, HITS);
	HealDamage(cast_on, points);
	healed := AP_GetVital(cast_on, HITS) - healed;

	SendSysMessage(caster, "You healed "+healed+" hit points of damage.");

	if ( caster.serial != cast_on.serial )
		SendSysMessage(cast_on, "You have had "+healed+" hit points of damage healed.");
	endif

	return 1;
endprogram