use uo;
use os;
use util;
use cfgfile;

include "include/client";
include "include/objtype";
include ":attributes:attributes";
include ":gumps:confirmationSizable";
include ":virtue:virtue";

const SFX_MEDITATION := SFX_FA;

var shrine_marker := GetObjtypeByName("shrine_marker");
var combatcfg := ReadConfigFile(":*:itemdesc");

program meditation(who)


	var poisoned := CInt( GetObjProperty(who,"poison_level") );
	if (poisoned >= 1)
		SendSysMessage( who, "You cannot meditate while poisoned." );
		return 0;
	endif

	var lastmed := CInt( GetObjProperty(who, "LastMeditation") );
	if (lastmed > ReadGameClock() )
		EraseObjProperty(who, "#ISMEDITATING");
		SendSysMessage(who, "You must rest for a bit.");
		return 0;
	endif

	if(who.warmode)
		SendSysMessage(who, "You cannot meditate in war mode");
		return 0;
	endif

	var loss := manaloss(who);
	if(loss == "nomed")
		SendSysMessage(who,"Regenerative forces cannot penetrate your armor.");
		return 0;
	endif

	var shrinelist := ListItemsNearLocationOfType(who.x, who.y, who.z, 3, shrine_marker, who.realm);
	Print(shrinelist);
	if( shrinelist.size() > 0 )
		if (confirmationSizable(who, "Do you wish to pray at the shrine of " + VS_Capitalize(GetObjProperty(shrinelist[1], "Virtue")) + "?", 88) )
			var theshrine := shrinelist[1];
			start_script( ":virtue:prayerScripts/" + (GetObjProperty(shrinelist[1], "Virtue") ), { who, GetObjProperty(theshrine, "Virtue") } );
			return 1;
		else
			PrintTextAbove(who, "Just meditating...");
		endif
	endif


	if ( GetAttribute(who, MEDITATION) < 5.0 )
		loss := SkillCheck( who, MEDITATION, 10, 50 );
		// This 'if' added to account for a negative 1 return by SkillCheck.
		if(loss < 0)
			loss := 0;
		endif
		SendSysMessage(who, "You cannot focus your concentration.");
		lastmed := CInt(ReadGameClock()) + 15;
		SetObjProperty(who, "LastMeditation", lastmed);
		EraseObjProperty(who, "#ISMEDITATING");
	endif

	var diff := CInt(GetAttribute(who, MEDITATION) - AP_GetVital(who, MANA)) + (loss / 1000);
	if (diff < 0)
		diff := 0;
	elseif (diff > 110)
		diff := 110;
	endif
	var pts := CInt(GetAttribute(who, MEDITATION) / 10) + 15;
	if(pts > 35)
		pts := 35;
	endif

//	if(SkillCheck( who, ATTRIBUTEID_MEDITATION, diff, (CInt(GetAttribute(who,ATTRIBUTEID_MEDITATION)) * 5) ) > 0)
	if(SkillCheck( who, MEDITATION, diff, pts ) > 0)
		var oldx := who.x;
		var oldy := who.y;
		var oldstamina := AP_GetVital(who, STAMINA);
		var chp := AP_GetVital(who, HITS);
		var cmana := AP_GetVital(who, MANA);
		lastmed := CInt(ReadGameClock()) + 60;
		SetObjProperty(who, "LastMeditation", lastmed);
		SendSysMessage(who, "You start to meditate.");
		PlaySoundEffect(who, SFX_MEDITATION);
		PrintTextAbove(who, "* Meditating *");
		SetObjProperty(who, "#ISMEDITATING", 1);

		while( (oldstamina <= AP_GetVital(who, STAMINA)) && (oldx==who.x) && (oldy==who.y) && (poisoned < 1) && (!who.warmode) && (!IsThingOnHand(who)) && (!who.hidden) )
			loss := manaloss(who);
			if(loss == "nomed")
				SendSysMessage(who,"Regenerative forces cannot penetrate your armor.", color := 33);
				EraseObjProperty(who, "#ISMEDITATING");
				return 0;
			endif
			if(AP_GetVital(who, MANA) >= AP_GetVitalMaximumValue(who, MANA))
				SendSysMessage(who, "Your mana has recharged.", color := 66);
				EraseObjProperty(who, "#ISMEDITATING");
				return 1;
			endif
			if((AP_GetVital(who, HITS) < chp) || (AP_GetVital(who, MANA) < cmana))
				SendSysMessage(who, "You lose your concentration.", color := 33);
				EraseObjProperty(who, "#ISMEDITATING");
				return 0;
			endif
			sleepms( CInt(50000 / GetAttribute(who, MEDITATION) ) );
			AP_SetVital(who, MANA, AP_GetVital(who, MANA) + 1);
			chp := AP_GetVital(who, HITS);
			cmana := AP_GetVital(who, MANA);
			oldstamina := AP_GetVital(who, STAMINA);
//			poisoned := CInt( GetObjProperty(who,"poison_level") );
			poisoned := who.poisoned;
		endwhile
		SendSysMessage(who, "You lose your concentration.", color := 33);
		EraseObjProperty(who, "#ISMEDITATING");
		return 0;
	else
		SendSysMessage(who, "You cannot focus your concentration.", color := 33);
		lastmed := CInt(ReadGameClock()) + 15;
		SetObjProperty(who, "LastMeditation", lastmed);
		EraseObjProperty(who, "#ISMEDITATING");
	endif

endprogram

//////////////////////////////////////////////////
// Checks for items equipped in the hands that might interfere with meditation.
//
// Legal items are: Black Staff, Shepherd's Crook, Gnarled Staff, Quarter Staff,
// 									Spellbook, Paladin Spellbook, Codex Damnorum (necro tome), Book of Arms and
//									Book of Spellweaving.
//
// Parameters: Mobile object reference
//
// Returned value: zero if no interferring items are in a hand or 1 if the item interferes.
//
//////////////////////////////////////////////////


function IsThingOnHand(who)
  var hand1 := (GetEquipmentByLayer(who, 1));
  var hand2 := (GetEquipmentByLayer(who, 2));
  if(hand1)
    if ((hand1.graphic == 0x0df0) || (hand1.graphic == 0x0e81) || (hand1.graphic == 0x13f9) || (hand1.graphic == 0x13f8)
		|| (hand1.graphic == 0x0e8a) || (hand1.graphic == 0x0efa) || (hand1.graphic == 0x2252) || (hand1.graphic == 0x2253)
		|| (hand1.graphic == 0x2254) || (hand1.graphic == 0x2D50))
	else
      SendSysMessage(who,"Your equipment prevents meditation.", color := 33);
      return 1;
	endif
  elseif (hand2)
    if ((hand1.graphic == 0x0df0) || (hand1.graphic == 0x0e81) || (hand1.graphic == 0x13f9) || (hand1.graphic == 0x13f8)
		|| (hand1.graphic == 0x0e8a) || (hand1.graphic == 0x0efa) || (hand1.graphic == 0x2252) || (hand1.graphic == 0x2253)
		|| (hand1.graphic == 0x2254) || (hand1.graphic == 0x2D50))
	else
      SendSysMessage(who,"Your equipment prevents meditation.", color := 33);
      return 1;
	endif
  else
    return 0;
  endif
endfunction

//////////////////////////////////////////////////////////////
// Checks equipped items for the member "medloss".
//
// Parameters: mobile object reference
//
// Returned value: Either the string "nomed" or the numeric value zero
///////////////////////////////////////////////////////////////

function manaloss(who)
//  var medit := GetAttribute(who,ATTRIBUTEID_MEDITATION);
  var elem;
  var loss := 0;
  foreach thing in ListEquippedItems(who)
    elem := FindConfigElem(combatcfg, thing.objtype);
    loss := GetConfigString(elem, "medloss");
    if(loss == "nomed")
      return loss;
      break;
    endif
  endforeach
  return 0;
endfunction