use uo;
use cfgfile;

include "include/objtype";
include ":attributes:attributes";
include "include/client";
include "include/canAccess";
include "include/toolWear";

var scissors_cfg := ReadConfigFile("scissors");
var cfg2 := ReadConfigFile("tailoring");
var cfg3 := ReadConfigFile("leather");

program use_scissors( who, scissors )
  EraseObjProperty(who, "IsMeditating");
  if(!can_access(who, scissors))
    return;
  elseif(scissors.movable == 0)
    SendSysMessage(who, "You cannot use that.");
    return;
  endif
  SendSysMessage(who, "Select an item to cut.");
  var cloth := Target(who);
  if(!cloth)
    SendSysMessage(who, "Cancelled.");
    return;
  elseif(!Accessible(who, cloth))
    SendSysMessage(who, "You cannot use that.");
    return;
  elseif(!can_access(who, cloth))
    return;
  elseif(cloth.movable == 0)
    SendSysMessage(who, "You cannot use that.");
    return;
  elseif(cloth in ListEquippedItems(who))
    SendSysMessage(who, "You cannot cut something being worn!");
    return;
  endif
  var material := Cint(scissors_cfg[cloth.objtype].material);
  var clr := cloth.color;
  var cont := cloth.container;
  var newitem := Cint(scissors_cfg[cloth.objtype].newitem);
  var amt, newcloth;
  if(material)
    amt := (material * cloth.amount);
    if(SubtractAmount(cloth, amt))
      newcloth := CreateItemAtLocation(who.x, who.y, who.z, newitem, amt);
      newcloth.color := clr;
      if (cont)
        MoveItemToContainer(newcloth, cont);
      else
        MoveItemToContainer(newcloth, who.backpack);
      endif
	  CheckToolWear (who, scissors, TAILORING);
      PlaySoundEffect(who, 0x0249);
      SendSysMessage(who, "You use the scissors to cut the material.");
      return;
    else
      SendSysMessage(who, "You can not cut that.");
      return;
    endif
  else
    material := Cint(cfg2[cloth.objtype].material);
    var leftover := 0x1766;
    if(!material)
      material := Cint(cfg3[cloth.objtype].material);
      leftover := 0x1067;
      if(!material)
        SendSysMessage (who, "You can't use scissors on that.");
        return;
      endif   
    endif
    PlaySoundEffect(who, 0x0249);
    amt := CInt((material * AP_GetSkill(who,TAILORING) * 75) / 10000);
    if(amt < 1)
      amt := 1;
    endif
    if(DestroyItem(cloth))
      newcloth := CreateItemInContainer(who.backpack, leftover, amt);
      CheckToolWear (who, scissors, TAILORING);
      if(newcloth)
        newcloth.color := clr;
      endif
    else
      SendSysMessage(who, "You can't use scissors on that.");
      return;
    endif
  endif
  
endprogram
