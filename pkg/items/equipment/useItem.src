use uo;

include ":equipment:equip";
include ":itemutils:itemUtil";
include ":itemutils:itemtypes";

program use_equipment( mobile, item )

	var equipped := 0;

	if( TypeOfInt( mobile ) == OT_ARRAY )
		item := mobile[2];
		mobile := mobile[1];
	endif

	if( item.IsA( POLCLASS_WEAPON ) || IsShield( item ) )
		// If the item in question has additional usages when double clicked,
		// this check allows you to bypass the in inventory check later on.
		// A bit of a kludge it, as the example below shows, allows for in this
		// instance, the player to select ammo if the item is equipped and is a bow.
		if( item == GetEquipmentByLayer( mobile, item.tile_layer ) )
			if( IsBow( item ) )
				// Start_Script( ":weapons:useScripts/bows/selectAmmo", { mobile, item } );
				Start_Script( ":weapons:bows/selectAmmo", { mobile, item } );
				return 1;
			else
				return 1;
			endif
		endif

		if( useItem_check( mobile, item, ITEM_INCONTAINER ) )
			equipped := EquipWeapon( mobile, item );
			if( equipped && IsBow( item ) )
				Start_Script( ":weapons:useScripts/selectAmmo", { mobile, item } );
			endif
		endif
	elseif( item.IsA( POLCLASS_ARMOR ) || item.IsJewelry() )
		if( useItem_check( mobile, item, ITEM_INCONTAINER ) )
			EquipArmor( mobile, item );
		endif
	endif

	return 1;
endprogram
