use vitals;
use uo;
use cfgfile;
use os;
use util;

include "include/canAccess";
include ":timedScripts:poisonUtil";
include ":timedScripts:timedScripts";

program use_magicfish(who, fish)
  EraseObjProperty(who, "IsMeditating");
  if(!can_access(who, fish))
    return;
  endif
  if(fish.movable == 0)
    SendSysMessage(who, "You cannot use that.");
    return;
  endif
  ReserveItem(fish);
  var spellnum := GetObjProperty( fish, "spellnum" );
  var spellsound := GetObjProperty( fish, "spellsound" );
  var spelleffect := GetObjProperty( fish, "spelleffect" );
  var miscprop1 := GetObjProperty( fish, "miscprop1" );
  var miscprop2 := GetObjProperty( fish, "miscprop2" );
  DestroyItem(fish);
  if(spellnum > 0)
	PlaySoundEffect( who, spellsound );
	PlayObjectCenteredEffect( who, spelleffect, 10,10);
	case (spellnum)
      4: HealDamage( who, miscprop1);
      6: who.setlightlevel( miscprop1, miscprop2 );
      9: TS_StartTimer(who, "Agility", miscprop2, miscprop1);
     10: TS_StartTimer(who, "Cunning", miscprop2, miscprop1);
     11:  if (TS_GetTimerNames(who, "P").size() > 0 )
			TS_CurePoison(who, 120, "P", CInt(miscprop1));
          endif
     16: TS_StartTimer(who, "Strength", miscprop2, miscprop1);
     17: TS_StartTimer(who, "Bless", miscprop2, miscprop1);
     44: who.hidden := 1;
    endcase
  else
    SendSysMessage(who, "Yech, that was a rotten fish!");
  endif
endprogram