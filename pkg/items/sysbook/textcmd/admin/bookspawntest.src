use uo;
use os;
use util;
use datafile;

program book_spawn_test(who);
  var shelf := Target(who);
  addbooks(shelf);
endprogram

function addbooks(bookcase)
  var count := 0;
  var books := EnumerateItemsInContainer(bookcase);
  var max := RandomDiceRoll("1d6+4");
  if(books.size()!=0)
    foreach book in books
     if(book.objtype == 0x7020)
        count:=count+1;
      endif
    endforeach
  endif
  while(count < max)
    if((RandomInt(5)+1) > 3)
      SpecialBook(bookcase);
    else
      CreateItemInContainer(bookcase, 0x7020, 1);
    endif
    count := count + 1;
  endwhile
endfunction

function SpecialBook(bookcase)
  var book := CreateItemInContainer(bookcase, 0xfef, 1);
  if(!book)
    return;
  endif
  case(RandomInt(4)+1)
    1: book.graphic := 4079;
    2: book.graphic := 4080;
    3: book.graphic := 4081;
    4: book.graphic := 4082;
  endcase
  var rint := CInt(RandomInt(30) + 1);
  var title := GetTitle(rint);
  SetObjProperty(book, "contents", GetContents(rint));
  SetObjProperty(book, "title",    title);
  SetObjProperty(book, "author",   GetAuthor(rint));
  book.name := title;
endfunction

function GetBookElem(bookid)
  var df := OpenDataFile("staticbooks");
  return df.FindElement(bookid);
endfunction

function GetContents(rint)
    return GetBookElem(rint).getprop( "contents" );
endfunction

function GetTitle(rint)
    return GetBookElem(rint).getprop( "title" );
endfunction

function GetAuthor(rint)
    return GetBookElem(rint).getprop( "author" );
endfunction