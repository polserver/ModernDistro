use uo;
use os;

exported function IsWritable(book)
  if(GetObjProperty(book, "NoWrite") || (book.movable == 0) || ((book.container).objtype == 0x0a9f))
    return 0;
  else
    if(book.movable == 0)
      return 0;
    endif
    return 1;
  endif
endfunction

exported function GetNumLines(book)
  return book.getprop("maxpages") * 8;
endfunction

exported function GetTitle(book)
  var res := book.getprop("title");
  if(res)
    return res;
  else
    return "";
  endif
endfunction

exported function SetTitle(book, title)
  title := nettoie(title);

  book.setprop("title", title);
  book.name := title;
endfunction

exported function GetAuthor(book)
  var res := book.getprop("author");
  if(res)
    return res;
  else
    return "";
  endif
endfunction

exported function SetAuthor(book, author)
  author := nettoie(author);

  book.setprop("author", author);
endfunction

exported function GetContents(book)
  var contents := book.getprop("contents");
  if(contents == error)
    contents := array;
  endif
  return contents;
endfunction

exported function GetLine(book, line)
  var contents := book.getprop("contents");
  if(contents == error)
    contents := array;
  endif
  if(line <= contents.size())
    var text := contents[line];
    if(text)
      return text;
    else
      return "";
    endif
  else
    return "";
  endif
endfunction

exported function SetLine(book, line, text)
  var contents := book.getprop("contents");
  if(contents == error)
    contents := array;
  endif
  contents[ line ] := nettoie(text);
  book.setprop("contents", contents);
  return 1;
endfunction

function nettoie(chaine)
 var tabchaine := CascZ(chaine);
 var result := "";
 var i := 1;
 var bon := 1;
 
 while (bon == 1)
  var car := tabchaine[i];
  if (car > 0)
   result := result + Cchr(car);
   i := i + 1;
  else
   bon := 0;
  endif
 endwhile
 
 return result;
endfunction

program BookInterface()
  return 1;
endprogram