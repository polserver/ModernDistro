/////////////////////////////////////////////////////////////////////
//
// setbooknottospawn
//
// Purpose:
// This allows the user to stop certain books in the Shard Library
// from being spawned in the bookshelves that are set to spawn
// books.
//
/////////////////////////////////////////////////////////////////////

use uo;
use file;
use cfgfile;
use datafile;

include ":sysbook:sysBook";
include ":gumps:gumps";

program setbooknottospawn(who)

	var book_titles := array;		// Array of titles to send to the gump.
	var selected_books := array;	// Array of books selected to be entered into the data file.
	var book_keys := array;			// Array of keys in the shard_library data file.
	var the_book := 0;				// Config file book objref.
	var book_title := "";			// String to hold the book title from the books.cfg file.
	var shard_library := RetrieveOrInitShardLibrary();

	if(shard_library.errortext)
		LogToFile("::log/sysbook.log", "[removefromlibrary.ecl]: Datastore file error: " + shard_library.errortext, LOG_DATETIME);
		Print("Sysbook error - check sysbook.log");
		return 0;
	endif
	// Build the list of titles that are not currently set to spawn for the selection gump.
	book_keys :=shard_library.Keys();
	foreach key in book_keys
		the_book := shard_library.FindElement(key);
		if(the_book.GetProp("spawn"))
			book_title := the_book.GetProp("title");
			book_titles.Append(book_title);
		endif
	endforeach
	book_titles.Sort();
	selected_books := SelectionGumpEx(who, CStr(book_titles.size()) + " books spawning in the library:", book_titles, BTN_TYPE_CHECKBOX);

	if(selected_books.errortext == "No option was selected.")
		SendSysMessage(who, "No books were selected.", color := 33);
		return 0;
	endif

	foreach book in selected_books
		foreach key in book_keys
			the_book := shard_library.FindElement(key);
			if(the_book.GetProp("title") == book)
				SendSysMessage(who, "Match " + book, color := 88);
				the_book.SetProp("spawn", 0);
			endif
		endforeach
	endforeach		// ...selected_title in selected_books
	SendSysMessage(who, "Finnished setting the selected books not to spawn in bookshelves.", color := 66);

endprogram
