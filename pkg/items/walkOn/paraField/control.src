/*
 * $Id: control.src 1162 2008-02-03 22:30:41Z AustinHeilman $
 *
 */

use uo;
use os;
use util;

include "include/client";
include ":timedScripts:timedScripts";

program controlScript(field)
	SetObjProperty(field, "#PID", GetPid());

	var creator := field.GetOwner();
	if ( creator.IsA(POLCLASS_MOBILE) )
		SetScriptController(creator);
	endif

	// Kludge to keep this script from running before cprops are set
	SleepMS(500);

	var duration := CInt(GetObjProperty(field, "ParaDuration"));
	if ( !duration )
		duration := RandomDiceRoll("5d3");
	endif

	while ( field )
		ParalyzeNearbyMobiles(field, duration);

		if ( ReadGameClock() % 5 == 0 )
			// Play once every 5 seconds.
			PlaySoundEffect(field, SFX_20C);
		endif

		var event := Wait_For_Event(1);
		if ( event )
			//Someone just walked onto the tile.
			ParalyzeMobile(event, duration);
		endif

		if ( field.IsExpired() )
			DestroyItem(field);
			return 1;
		endif
	endwhile
endprogram

function ParalyzeNearbyMobiles(field, duration)
	var nearby := ListMobilesNearLocationEX(field.x, field.y, field.z, 0, LISTEX_FLAG_NORMAL+LISTEX_FLAG_HIDDEN, field.realm);
	foreach mobile in ( nearby )
		ParalyzeMobile(mobile, duration);

		SleepMS(2);
	endforeach

	return (nearby.Size());
endfunction

function ParalyzeMobile(mobile, duration)
	TS_StartTimer(mobile, "Paralysis", duration);

	return 1;
endfunction
