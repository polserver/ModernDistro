/*
        Maintained by The POL Distro Team

        2018-07-25
*/
use uo;
use cfgfile;
use util;

program OnRemove( unused character, container, unused item, unused item_amount, unused movetype )
	var shelf_cfg := ReadConfigFile( ":containers:bookshelf" );
	if( !shelf_cfg )
		return 1;
	endif

	shelf_cfg := shelf_cfg[container.graphic];

	var num_items := EnumerateItemsInContainer( container ).Size();
	if( num_items > 0 )
		// There are still other books left in the shelf.
		// Change the book shelf to a random compatable filled graphic.
		// Ooo special effects.
		var graphics := GetConfigStringArray( shelf_cfg, "FilledGraphic" );
		container.graphic := graphics.RandomEntry();
	else
		// No more books left - set to an empty shelf graphic.
		container.graphic := shelf_cfg.EmptyGraphic;
	endif

	return 1;
endprogram
