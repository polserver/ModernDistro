use uo;
use os;
use util;
use cfgfile;

include "include/eventID";
include "include/mrcSpawn";
include "include/NPCUtil";
include "include/sysEvent";
include ":body:bodyHues";
include ":npcs:randName";

var npclist := Array;
var spawn, amt, item, status, rng, tmplate;
var npccfg := ReadConfigFile("::npcdesc");

program merchantnode(itm)
	item := itm;
	SetObjProperty(item, "#Pid", GetPid());
	item.color := 53;
	item.invisible := 1;
	SetObjProperty(item, "#wait", 1);
	SetNodeName("initializing");
	sleep(RandomInt(3));

// Assign Merchant Node serial to the npc as a cprop.
	var npc := 0;
	foreach serial in ( GetObjProperty( item, "NpcList" ) )
		npc := SystemFindObjectBySerial( serial );
		if( npc.IsA( POLCLASS_NPC ) )
			if( GetObjProperty( npc, "MerchantNode" ) )
				continue;
			endif

			SetObjProperty( npc, "MerchantNode", item.serial );
		endif
	endforeach
// if all nodes are globally disabled, we sleep until they are enabled again.
	if(GetGlobalProperty("nomerchants"))
		SetNodeName("globally disabled");
		SetObjProperty(item, "#wait", 2);
		while(GetGlobalProperty("nomerchants"))
			sleep(5);
		endwhile
	endif

//
	SetNodeName("enabled");
	EraseObjProperty(item, "#wait");
	rng := CInt(GetObjProperty(item, "Range"));
	if (!rng )
		rng := 5;
		SetObjProperty(item, "Range", rng);
	endif
	item.color := 0;

// construct a list of npcs in an array.
	foreach entry in GetObjProperty(item, "NpcList");
		var critter := SystemFindObjectBySerial(entry);
		if(critter)
			if( !(critter in npclist) )
				npclist.append(critter);
			endif
		endif
		sleepms(10);
	endforeach

	spawn := GetObjProperty(item, "Type");

	if(!spawn)
		var holder := array;
		var tmparray := array;
		var template := GetObjProperty(item, "Template");
		foreach thing in npclist
			tmparray := array;
			tmparray.append(thing.npctemplate);
			var anchor := GetObjProperty(thing, "Anchor");
			tmparray.append(anchor);
			tmparray.append(1);
			holder.append(tmparray);
			if((!template) || (template == 1) || (template == "merchant"))
				SetObjProperty(item, "Template", 1);
				var merctype := FindMerchantType(tmparray[1]);
				create_node_merc(merctype, merctype + " " + anchor[1] + " " + anchor[2] + " " + anchor[3]);
			endif
			DestroyRootItemInStorageArea(storage, thing.serial + " FS");
			DestroyRootItemInStorageArea(storage, thing.serial + " PB");
			DestroyRootItemInStorageArea(storage, thing.serial + " 1C");
			Sleep(2);
		endforeach
		spawn := holder;
		SetObjProperty(item, "Type", spawn);
	endif

//
	ValidateSpawns();
	amt := len(spawn);
	if(!GetObjProperty(item, "Status"))
		SetObjProperty(item, "Status", "on");
	endif
	var gc := 0;
	RegisterForSpeechEvents(item, rng);
	EnableEvents(SYSEVENT_SPEECH, rng);
	var nxt := CInt(GetObjProperty(item, "Restock")) * 60;
	var next_restock	:= ReadGameClock() + nxt;
	if(!GetObjProperty(item, "Status"))
		SetObjProperty(item, "Status", "on");
	endif
	var template := GetObjProperty(item, "Template");
	SetObjProperty(item, "#FirstRestock", 1);
	var parms := array;
	parms.append(item);
	parms.append(1);
	start_script("restock", parms);
	EraseObjProperty(item, "#FirstRestock");

// This is the main loop for listening to events and sending them to the merchants
	while(item)
		if((GetObjProperty(item, "Status") == "on") && (!GetGlobalProperty("nomerchants")))
			// only process if the node is still enabled
			spawn := GetObjProperty(item, "Type");
			if(ReadGameClock() >= gc)
				ValidateSpawns();
				SpawnAll(item.realm);
				ValidateSpawns();
				gc := ReadGameClock() + 120;
			endif
			var ev := os::wait_for_event(30);
			case(ev.type)
				SYSEVENT_SPEECH:
						if(template == 2)
							ProcessBankerText(ev);
						else
							ProcessMerchantText(ev);
						endif
				"Respawn":	spawn := GetObjProperty(item, "Type");
						ValidateSpawns();
						SpawnAll(item.realm);
						ValidateSpawns();
				"Restock":	RestockAllMerchants();
			endcase
			if(ReadGameClock() > next_restock)
				if(template == 1)
					start_script("restock", item);
				endif
				next_restock	:= ReadGameClock() + nxt;
			endif
		else
			// Merchants with this node have been turned off (locally or globally)
			item.color := 53;
			if(GetObjProperty(item, "Status") == "off")
				SetNodeName("disabled");
			else
				SetNodeName("globally disabled");
			endif
			DespawnAll();
			ValidateSpawns();

			// hang out here until it gets turned back on again.
			while((GetObjProperty(item, "Status") == "off") || (GetGlobalProperty("nomerchants")))
				var ev := os::wait_for_event(120);
				ev := ev;
			endwhile

			// Things have turned back on, so this stuff gets moving
			SpawnAll(item.realm);
			ValidateSpawns();
			item.color := 0;
			SetNodeName("enabled");
			sleep(1);
		endif
	endwhile
endprogram

function SetNodeName(txt)
	var template := GetObjProperty(item, "Template");
	if((!template) || (template == "merchant"))								 // setting to merchant mode
		SetObjProperty(item, "Template", 1);
	elseif(template == "banker")																// setting to banker mode
		SetObjProperty(item, "Template", 2);
	endif
	if(template == 1)
		item.name := "a merchant node [" + txt + "]";
	else
		item.name := "a banker node [" + txt + "]";
	endif
endfunction

function ValidateSpawns()
	var chk := array;
	foreach entry in spawn
		foreach thing in npclist
			if(entry[2] == GetObjProperty(thing, "Anchor"))
				var npc := thing;
				if((entry[3] == 1) && (AP_GetVital(npc, VITALID_LIFE)))
					if(npc in chk)
						// do nothing -- invalid entry --
					else
						chk.append(npc);
					endif
				else
					MoveObjectToLocation(npc, 6066, 1, 0, "britannia", MOVEOBJECT_FORCELOCATION);
					RevokePrivilege(npc, "invul");
					npc.Kill();
				endif
			endif
			sleepms(2);
		endforeach
		sleepms(2);
	endforeach
	foreach npc in npclist
		if(npc in chk)
			// do nothing;
		else
			MoveObjectToLocation(npc, 6066, 1, 0, "britannia", MOVEOBJECT_FORCELOCATION);
			RevokePrivilege(npc, "invul");
			npc.Kill();
		endif
		sleepms(2);
	endforeach
	npclist := chk;
	var holder := array;
	foreach thing in npclist
		holder.append(thing.serial);
	endforeach
	SetObjProperty(item, "NpcList", holder);
endfunction

function DespawnAll()
	var chk := array;
	chk := chk;
	foreach thing in npclist
		var hp := AP_GetVital(thing, VITALID_LIFE);
		if(hp)
			MoveObjectToLocation(thing, 6066, 1, 0, "britannia", MOVEOBJECT_FORCELOCATION);
			RevokePrivilege(thing, "invul");
			thing.Kill();
		endif
		sleepms(2);
	endforeach
	npclist := array;
	SetObjProperty(item, "NpcList", npclist);
endfunction

function SpawnAll(map)

	foreach thing in spawn
		var type	 := thing[1];
		var root1	 := thing[2];
		var status1 := thing[3];
		// var chest	:= thing[4];
		var chk := 0;
		foreach npc in npclist
			if(root1 == GetObjProperty(npc, "Anchor"))
				chk := 1;
			endif
		endforeach
		if((chk == 0) && (status1 == 1))
			var new := 0,
				props := MerchantSetup( type, root1, map );
			while( ( !new ) && item )
				new := CreateNpcFromTemplate( type, root1[1], root1[2], root1[3], props, map );
				if( new.IsA( POLCLASS_NPC ) )
					npclist.Append( new );
				else
					Print( item.serial+" Failed to Spawn Merchant at: "+root1[1]+" "+root1[2]+" "+root1[3]+" "+map+" -> "+new.errortext );
				endif
				Sleep( 20 );
			endwhile
		endif
	endforeach
endfunction

// ToDo: Assigning specific name to a merchant via an optional parameter which will likely be a struct
// containing gender, name
function MerchantSetup( npctemplate, root1, map )

	var color := RandomHumanSkinHue(),
		gender := RandomInt( 2 ),
		template_elem := NPC_GetNPCConfig( npctemplate );

	var name := ( !gender ? FetchRandomName( "HumanMale" ) : FetchRandomName( "HumanFemale" ) ),
		title := PreserveTitle( template_elem.Name );

	var props := Struct{
						"Name" := name+title,
						"Color" := color,
						"TrueColor" := color,
						"Gender" := gender,
						"Objtype" := ( ( gender == 0 ) ? 0x190 : 0x191 ),
						"Graphic" := ( ( gender == 0 ) ? 0x190 : 0x191 ),
						"Facing" := RandomInt( 8 ),
						"Speech_Font" := 0x3,
						"Speech_Color" := RandomIntMinMax( 2, 1000 ),
						"CProps" := Dictionary {
							"Anchor" -> { root1[1], root1[2], root1[3], map },
							"MerchantNode" -> item.serial
							}
						};

	return props;
endfunction

function ProcessBankerText(ev)
	var text := lower(ev.text);
	var who := ev.source;
	var npc := ev.npc;
	var evnt := array;
	evnt.+ type;
	evnt.+ source;
	evnt.+ text;
	if(text["bankers note"] || text["banker's note"] || text["bankers order"] || text["banker's order"] || text["bank draft"] || text["balance"] || text["bank"])
		var dis := 15;
		if( !npc.IsA( POLCLASS_NPC ) )
			foreach mob in npclist
				if(CheckLineOfSight(ev.source, mob))
					var dst := Distance(who, mob);
					if(dst < dis)
						dis := dst;
						npc := mob;
					endif
				endif
			endforeach
		endif

		evnt.type := EVID_NODE;
		evnt.source := ev.source;
		evnt.text := ev.text;
		if (npc)
			SendEvent(npc, evnt);
		endif
	endif
endfunction

function ProcessMerchantText(ev)
	var text := lower(ev.text);
	var who := ev.source;
	var npc := ev.npc;
	var pass := 0;
	var evnt := array;
	evnt.+ type;
	evnt.+ source;
	evnt.+ text;
	// These speech items require no other speech, so should be included in all checks.
	if (text["appraise"] || text["stable"] || text["showinventory"] || text["showrestock"])
		pass := 1;
	endif

	// If the character wants to do something with a vendor:
	if (text["buy"] || text["sell"] || text["job"] || text["train"] || text["teach"] || (pass))

		// Just pick the nearest vendor if the player isn't specific.
		if( !npc.IsA( POLCLASS_NPC ) )
			if( text["merchant"] || text["vendor"] || (pass) )
				var dis := 7;
				foreach mob in npclist
					if(CheckLineOfSight(who, mob))
						var dst := Distance(who, mob);
						if(dst < dis)
							dis := dst;
							npc := mob;
						endif
					endif
				endforeach
			endif


		// Has the player said the name of the vendor?  Use that.
			var vendorname;
			foreach mob in npclist
				if(CheckLineOfSight(who, mob))
					vendorname := splitwords(mob.name);
					if (text[lower(vendorname[1])])
						npc := mob;
					endif
				endif
			endforeach
		endif
	endif
	if (!npc)
		return;
	endif

	if(Distance(who, npc) <= 4)
		evnt.type := EVID_NODE;
		evnt.source := ev.source;
		evnt.text := ev.text;
		SendEvent(npc, evnt);
	endif
	return;
endfunction

function RestockAllMerchants()
	if((!GetGlobalProperty("norestock")) && (!GetGlobalProperty("nomerchants")))
		var cratelist := GetObjProperty(item, "Type");
		foreach entry in cratelist
			var anchor		:= entry[2];
			var stockname := FindMerchantType(entry[1]) + " " + anchor[1] + " " + anchor[2] + " " + anchor[3];
			var inv_fs := FindRootItemInStorageArea(storage, stockname + " FS");
			var inv_rs := FindRootItemInStorageArea(storage, stockname + " RS");
			if(inv_rs && inv_fs)
				checkstock(inv_rs, inv_fs);
				RestockMyInventory(inv_rs, inv_fs);
			endif
			sleepms(10);
		endforeach
	endif
endfunction
