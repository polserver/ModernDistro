use uo;
use os;

include "include/eventID";
include ":contextmenus:constants";
include ":contextmenus:contextMenu";
include ":contextmenus:events";

program context_menu( parameters )

	var mobile := parameters[1],
		obj := parameters[2];

// This function stops any previous context menu script actively running,
// without this if the CM is closed by clicking outside the menu OR
// by right clicking the menu to close it, the next context menu
// does not return any value and the user needs to use the context
// menu an additional time to actually use any options within a context
// menu.
	ContextMenu_ValidateMenu( mobile );

	var list := obj.GetContextMenuEntries( mobile );
	if( !list.Size() || TypeOfInt( list ) != OT_ARRAY )
		return 0;
	endif

	var context_menu := SendPopUpMenu( mobile, obj, list ),
		ret := 0;
	// Print( "Raw Return: "+context_menu );
	if( !context_menu )
		// Print( "And I'm Out!" );
		return 0;
	endif

	ret := list[context_menu];
	if( TypeOfInt( ret ) == OT_STRUCT )
		if( ret.disabled == 1 )
			return 0;
		endif
		ret := ret.cliloc;
	endif

	case( ret )
		3006103:	ContextMenu_SendMerchantNodeEvent( mobile, obj, CM_VENDOR_BUY );
		3006104:	ContextMenu_SendMerchantNodeEvent( mobile, obj, CM_VENDOR_SELL );
		3006105:	ContextMenu_SendMerchantNodeEvent( mobile, obj, CM_OPEN_BANK );
		3006107:	ContextMenu_SendNPCEvent( mobile, obj, CM_GUARD_ME );
		3006108:	ContextMenu_SendNPCEvent( mobile, obj, CM_FOLLOW_ME );
		3006109:	ContextMenu_SendNPCEvent( mobile, obj, CM_DROP );
		3006111:	ContextMenu_SendNPCEvent( mobile, obj, CM_KILL );
		3006112:	ContextMenu_SendNPCEvent( mobile, obj, CM_STOP );
		3006113:	ContextMenu_SendNPCEvent( mobile, obj, CM_TRANSFER );
		3006114:	ContextMenu_SendNPCEvent( mobile, obj, CM_STAY );
		3006115:	ContextMenu_HandleResignEvent( mobile, obj );
		3006116:	ContextMenu_HandleJoinEvent( mobile, obj );
		3006118:	ContextMenu_SendNPCEvent( mobile, obj, CM_RELEASE );
		3006123:	OpenPaperdoll( mobile, obj );
		3006124:	ContextMenu_SendMerchantNodeEvent( mobile, obj, CM_CHECK_BALANCE );
		3006126:	ContextMenu_SendMerchantNodeEvent( mobile, obj, CM_STABLE );
		3006145:	ContextMenu_OpenPack( mobile, obj );
		3006201:	ContextMenu_InsureItem( mobile );
		1114299:	ContextMenu_InsuranceMenu( mobile );
		default:	return 1;
	endcase

	return 1;
endprogram
