use os;
use uo;

Const UOBJECT_BRASS_SIGN := 0x0bd2;
Const UOBJECT_WOOD_SIGN := 0x0bd0;

exported function CloseCustomHouse ( who, house)
	if (who.concealed < 2)
		who.concealed := 0;
	endif	
	var components := GetObjProperty (house, "components");
	var lockid := 0;
	foreach compo in (components)
		var sign := SystemFindObjectBySerial(compo);
		if (sign.objtype == UOBJECT_BRASS_SIGN or sign.objtype == UOBJECT_WOOD_SIGN)
			lockid := GetObjProperty (sign, "lockid");
			if (lockid)
				break;
			endif
		endif
	endforeach
	var teleporters := array{};
	foreach item in (house.components)
		if (item.isa (POLCLASS_DOOR))
			item.locked := 1;
			SetObjProperty (item, "lockid", lockid);
		elseif (item.objtype >= 0x181d and item.objtype <= 0x1828)
			teleporters.append(item);
		endif
	endforeach
	foreach tele in teleporters
		foreach nexttele in teleporters
			if (_nexttele_iter <= _tele_iter)
				continue;
			endif
			if (tele.objtype == nexttele.objtype)
				SetObjProperty (tele, "DestX", nexttele.x);
				SetObjProperty (tele, "DestY", nexttele.y);
				SetObjProperty (tele, "DestZ", nexttele.z);
				SetObjProperty (tele, "DestR", nexttele.realm);
				break;
			endif
		endforeach
		if (!GetObjProperty (tele, "DestX"))
			foreach nexttele in teleporters
				if (_nexttele_iter >= _tele_iter)
					break;
				endif
				if (tele.objtype == nexttele.objtype)
					SetObjProperty (tele, "DestX", nexttele.x);
					SetObjProperty (tele, "DestY", nexttele.y);
					SetObjProperty (tele, "DestZ", nexttele.z);
					SetObjProperty (tele, "DestR", nexttele.realm);
					break;
				endif
			endforeach
		endif
	endforeach
endfunction

program SyshookCloseCustomHouse ()
	Syslog ("Installing CloseCustomHouse syshook...");
	return 1;
endprogram