use uo;

include "include/eventID";
include "include/innCheck";
include "include/housing";

program logofftest(who)

	// Need the house sign for safe logoff determination later.
	var hsign := GetHouseSign( who );

	if((who.cmdlevel > 1) || (GetObjProperty(who,"justbanned")))
		return 0;
	endif
	EraseObjProperty(who, "#Online");
	var safelogoff := 0;
	if (CInt(GetObjProperty(who, "#camped"))== 1)
		EraseObjProperty(who, "#camped");
		safelogoff := 1;
	elseif(Inncheck(who))
		safelogoff := 1;
	// Determines safe logoff for housing systems.
	elseif(hsign.IsAffiliated(who))
		safelogoff := 1;
	endif
  if(!safelogoff)
    staffnotify(who);
    return 300;  // wait 5 minutes
  elseif(safelogoff == 1)
    return 0;   // ok to log off
  else
    return safelogoff;
  endif
endprogram

function staffnotify(who)
  var pid := 0;
  while(!pid)
    sleepms(50);
    pid := GetGlobalProperty("#stafflist");
  endwhile
  pid := getprocess(pid);
  if(!pid)
    return;
  endif
  var k := struct;
  k.+player := who;
  k.+type  := EVID_PLAYER_DELAY;
  pid.sendevent(k);
endfunction