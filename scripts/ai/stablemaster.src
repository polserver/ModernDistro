use npc;
use basic;
use os;
use uo;

include "include/eventID";
include ":attributes:attributes";
include "include/sysEvent";
include "include/randName";
include "include/utility";
include "include/mrcSpawn";
include "include/speech";
include "include/client";
include "include/vetement";
include "include/NPCBackpacks";
include "util/bank";
include "include/anchors";
include "include/dist";
include "include/findCity";
include "include/begging";
include "include/trainSkill";

const RESPAWN_MINUTES := 30;     // In how many minutes will the NPC respawn if killed.
const MAX_SKILLS      := 48;     // There are currently 48 Skills.
const UOBJ_GOLDCOIN   := 0x0eed; // A gold coin
const UOBJ_METALCHEST := 0x0e7c; // A Metal Chest
const REACT_THRESHOLD := 2;

set_priority( 50 );

var me := Self();
me.hidden := 1;
var buyimage  := {0x211b, 0x211c, 0x2121, 0x2124, 0x211f, 0x2120, 0x2126, 0x20f6, 0x211a, 0x2123, 0x2125, 0x2122, 0x211e, 0x211d, 0x2119, 0x20e6, 0x2108, 0x2103, 0x2101, 0x2135, 0x2136, 0x2137, 0x2127};
var sellimage := {0xc9, 0xd9, 0xcc, 0xc8, 0xe2, 0xe4, 0x123, 0xdc, 0x06, 0xee, 0xcd, 0xe1, 0xd4, 0x05, 0xd6, 0xcf, 0xd1, 0xd8, 0xcb, 0xd2, 0xda, 0xdb, 0x124};
var speech, inv_rs, inv_fs, inv_pb, inv_1c;
var merchant_type := GetObjProperty(me, "MerchantType");
var npccfg := ReadConfigFile( "npcdesc" );
var mystorage := GetObjProperty(me, "Pets");
var player, counter, pet, mx := 5306, my := 1164;
if(!merchant_type)
  SetObjProperty(me, "MerchantType", "stable");
  merchant_type := "stable";
endif

program stablemaster()
  start_script("NPCKeeper", me);
  var time := ReadGameClock();
  SetObjProperty(me,"countercheck", time);
  var parms := {};
  parms[1] := me;
  parms[2] := time;
  start_script(":taming:stableCounter", parms);
  var myanchor := GetObjProperty(me, "Anchor");
  if(myanchor)
    MoveObjectToLocation(me, myanchor[1], myanchor[2], myanchor[3], me.realm, MOVEOBJECT_FORCELOCATION);
  endif
  drop_anchor();
  myanchor     := GetObjProperty(me, "Anchor");
  var spawnname := merchant_type + " " + myanchor[1] + " " + myanchor[2] + " " + myanchor[3];
  inv_fs := FindRootItemInStorageArea(storage, spawnname + " FS");
  inv_pb := FindRootItemInStorageArea(storage, spawnname + " PB");
  inv_1c := FindRootItemInStorageArea(storage, spawnname + " 1C");
  inv_rs := FindRootItemInStorageArea(storage, spawnname + " RS");
  while((!inv_rs) or (!inv_fs) or (!inv_pb) or (!inv_1c))
    sleep(5);
    inv_fs := FindRootItemInStorageArea(storage, spawnname + " FS");
    inv_pb := FindRootItemInStorageArea(storage, spawnname + " PB");
    inv_1c := FindRootItemInStorageArea(storage, spawnname + " 1C");
    inv_rs := FindRootItemInStorageArea(storage, spawnname + " RS");
  endwhile
  me.hidden := 0;
  EnableEvents(EVID_NODE);
  EnableEvents(SYSEVENT_ITEM_GIVEN);
  DisableEvents(SYSEVENT_SPEECH);
  var next_wander := ReadGameClock() + 10;
  if(GetObjProperty(me, "frozen"))
    me.frozen := 1;
  endif
  counter := GetObjProperty(me, "counter");
  if(!counter)
    counter := 0;
  endif
  if(!mystorage)
    mystorage := array;
    SetObjProperty(me, "Pets", mystorage);
  endif
  var ev, text;
  var petname;
  var newpet;
  while (1)
    ev := os::wait_for_event(120);
    if(ev)
      mystorage := GetObjProperty(me, "Pets");
      case (ev.type)
        EVID_NODE:                text := Lower(ev.text);
                                  if((ev.source.cmdlevel > 2) && (text["showinventory"]))
                                    SendOpenSpecialContainer(ev.source, inv_fs);
                                  endif
                                    if(text["buy"])
                                      TurnToward(ev.source);
                                      var res;
                                      res := SendBuyWindow(ev.source, inv_fs, Self(), inv_pb );
                                      if (!res)
                                      endif
                                    elseif (text["sell"])
                                      if(GetObjProperty(inv_rs, "MyGold") >= 1000)
                                        TurnToward(ev.source);
		                                sellanimal(ev.source);
                                      else
                                        PrintTextAbovePrivate(Self(), "I cannot afford any more of that", ev.source );
                                      endif
                                    elseif((text["vendor train"]) || (text["vendor teach"]) || (text["merchant train"]) || (text["merchant teach"]))
                                      TurnToward(ev.source);
                                      MerchantTrain(me, ev.source, text);
                                    elseif(ev.text["stable"])
                                    if(len(mystorage) >= 50)
                                      say("Im afraid im booked solid at the moment, please come again later");
                                    else
                                      player := ev.source;
                                      TurnToward(ev.source);
                                      say("Show me your pet.");
                                      pet := Target(player);
                                      if (pet)
                                        var tempdata := GetObjProperty(pet, "master");
                                        var summoned := GetObjProperty(pet, "summoned");
                                        if((pet.script == "tamed") && (!summoned))
                                          if (tempdata == player.serial)
                                            var price := 30 + (GetStrength(pet)/5);
                                            say("I charge " + price + " per real world week to take care of " + pet.name + ".");
                                            if (GetPayment(player, price))
                                              say("Keep this ticket and give it to me when you want " + pet.name + " back.");
                                              var ticket := CreateItemInBackpack(player, 0x14f0, 1);
                                              ticket.usescript := "";
                                              Save_Pet_Data(ticket, pet);
                                            endif
                                          else
                                            say("This is not a zoo!");
                                          endif
                                        elseif((pet.graphic == CID_HUMAN_MALE) || (pet.graphic == CID_HUMAN_FEMALE) )
                                          say("Do I look like an Inn Keeper?!");
                                        else
                                          say("That is not your pet.");
                                        endif
                                      endif
                                    endif
                                  endif
        SYSEVENT_MERCHANT_BOUGHT: TurnToward(ev.source);
                                  PrintTextAbovePrivate(Self(), "The total of thy sale is " + ev.amount, ev.source );
                                  var mygold := GetObjProperty(inv_rs, "MyGold");
                                  mygold := mygold - ev.amount;
                                  SetObjProperty(inv_rs, "MyGold", mygold);
                                  foreach item in EnumerateItemsInContainer(inv_pb )
                                    DestroyItem(item);
                                  endforeach
                                  PlaySoundEffect(me, 0x38);
        SYSEVENT_MERCHANT_SOLD:   TurnToward(ev.source);
                                  PrintTextAbovePrivate(Self(), "The total of thy purchase is " + ev.amount, ev.source );
                                  var mygold := GetObjProperty(inv_rs, "MyGold");
                                  mygold := mygold + ev.amount;
                                  SetObjProperty(inv_rs, "MyGold", mygold);
                                  foreach item in EnumerateItemsInContainer(ev.source.backpack )
		                            petname :=GetObjProperty(item, "animalkind");
		                            if(petname)
		                              newpet := CreateNpcFromTemplate(petname, ev.source.x, ev.source.y,ev.source.z, 0, me.realm);
		                              DestroyItem(item);
                                      SetObjProperty(newpet, "master", ev.source.serial);
                                      SetObjProperty(newpet, "prevtamed", 1);
		                              newpet.script := "tamed";
                                      RestartScript(newpet);
		                            endif
                                  endforeach
                                  PlaySoundEffect(me, 0x38);
        SYSEVENT_ITEM_GIVEN:      if(ev.item.objtype == UOBJ_GOLDCOIN);
                                    TrainSkill(me, ev.source, ev.item);
		                          else
		                            TurnToward(ev.source);
		                            Load_Ticket_Data(ev.source, ev.item);
		                          endif
      endcase
    endif
	if(ReadGameClock() >= next_wander)
	  begpurse(me);
      wander();
      if(coordist(me.x, me.y, myanchor[1], myanchor[2]) > 5)
        MoveObjectToLocation(me, myanchor[1], myanchor[2], myanchor[3], me.realm, MOVEOBJECT_FORCELOCATION);
      endif
      next_wander := ReadGameClock() + 30;
    endif
    Lookiehere(me);
  endwhile
endprogram

function sellanimal(who)
  var i;
  say("What pet would you like to sell?");
  var pet := Target(who);
  if((!pet) || (pet==""))
	SendSysMessage(who, "Cancelled.");
	return;
  endif
  var tempdata  := GetObjProperty(pet, "master");
  var summoned  := GetObjProperty(pet, "summoned");
  if(!tempdata)
	say("I cannot buy that.");
	return;
  endif
  if((pet.script == "tamed") && (tempdata == who.serial) && (!summoned))
    var theitem;
	if((pet.objtype==0xd3) && (pet.color==0x02e6))
	  theitem := CreateItemInContainer(inv_fs, 0x20cf, 1);
	elseif(pet.objtype==0xd3)
	  theitem := CreateItemInContainer(inv_fs, 0x2118, 1);
	else
	  for(i:=1;i<=sellimage.size();i:=i+1)
		if(pet.objtype==sellimage[i])
		  theitem := CreateItemInContainer(inv_fs, buyimage[i], 1);
		  break;
		endif
	  endfor
	endif
    SetObjProperty(theitem, "ClearRestock", (ReadGameClock() + 1800));
	var buyamount := CInt(GetStrength(pet) / 2);
    var mygold := GetObjProperty(inv_rs, "MyGold");
    if(buyamount > mygold)
      say("Im sorry, but I cannot afford to buy any pets right now.");
      return;
    else
      mygold := mygold - buyamount;
      SetObjProperty(inv_rs, "MyGold", mygold);
	  say(pet.name + " is worth $" + buyamount);
	  SetObjProperty(pet, "guardkill", 1);
	  MoveObjectToLocation(pet, 5555, 1111, 0, "britannia_alt", MOVEOBJECT_FORCELOCATION); // new map problem
	  ApplyRawDamage(pet, GetHp(pet) + 10);
	  CreateItemInBackpack(who, 0x0eed, buyamount);
	  return;
    endif
  elseif((pet.graphic == CID_HUMAN_MALE) || (pet.graphic == CID_HUMAN_FEMALE) )
	say("Do I look like an Inn Keeper?!");
  else
	say("You could sell " + pet.name + " but that is not your pet.");
  endif
endfunction

function Save_Pet_Data(ticket, pet)
  var tempdata;
  ticket.name := "Pet claim ticket for : " + pet.name;
  SetObjProperty(ticket, "StableMaster", me.serial);
  SetObjProperty(ticket, "petserial", pet.serial);
  SetObjProperty(ticket, "pettemplate", pet.npctemplate);
  SetObjProperty(pet,"stabled", 1);
  SetObjProperty(pet,"days", 1);
  GrantPrivilege(pet, "invul");
  pet.enable("invul");
  MoveObjectToLocation(pet, mx, my, 0, pet.realm, MOVEOBJECT_FORCELOCATION);
  mystorage.append(pet.serial);
  SetObjProperty(me, "Pets", mystorage);
  SetObjProperty(pet, "NextCounter", ReadGameClock() + 86400);
endfunction

function Load_Ticket_Data(player, ticket)
  if(GetObjProperty(ticket, "StableMaster") != me.serial)
    PrintTextAbovePrivate(me, "This doesnt seem to be one of my tickets, please take it to the stable master who issued it.", player);
    MoveItemToContainer(ticket, player.backpack);
    return;
  elseif(!canclaim(player,GetObjProperty(ticket, "pettemplate")))
    say("You have no chance of controlling that!");
    return;
  endif
  var tempdata := 0;
  var newpet := 0;
  var pet;
  if(ticket.objtype == 0x14f0)
    tempdata := GetObjProperty(ticket, "petserial");
    say("Oh, let me find your pet for you. One moment, please.");
    sleep(1);
    var petserial := GetObjProperty(ticket, "petserial");
    mystorage := GetObjProperty(me, "Pets");
    if(petserial in mystorage)
      pet := SystemFindObjectBySerial(petserial);
    endif
    if(!pet)
      say("Im sorry, but I dont have this pet,");
      sleep(1);
      say("perhaps you may have left him here too long?");
      DestroyItem(ticket);
      return;
    endif
    SetObjProperty(pet, "master", player.serial);
    SetObjProperty(pet, "prevtamed", 1);
    EraseObjProperty(pet,"stabled");
    EraseObjProperty(pet,"days");
	pet.script := "tamed";
    RestartScript(pet);
    RevokePrivilege(pet, "invul");
    MoveObjectToLocation(pet, player.x, player.y, player.z, player.realm, MOVEOBJECT_FORCELOCATION);
    var holder := array;
    mystorage := GetObjProperty(me, "Pets");
    foreach thing in mystorage
      if(thing != pet.serial)
        holder.append(thing);
      endif
    endforeach
    mystorage := holder;
    SetObjProperty(me, "Pets", mystorage);
    EraseObjProperty(pet, "NextCounter");
    say("Take care of " + pet.name + " and be sure to feed it!");
    DestroyItem(ticket);
  else
    TakeItem(player, ticket);
  endif
endfunction

function GetPayment(player, price)
  var backpack := EnumerateItemsInContainer(player.backpack);
  var a := 0;
  foreach item in backpack
    if ((item.objtype == 0xeed) && (item.amount >= price) )
      if(SubtractAmount(item, price))
        a := 1;
        break;
      endif
    endif
  endforeach
  if(a == 1)
    return 1;
  else
    say("You dont have " + price + " gold coins!");
    return 0;
  endif
endfunction

function canclaim(player, pettemplate)
  case (pettemplate)
    "horse":
    "horse1":
    "horse2":
    "horse3":
    "forestostard":
    "desertostard":
    "llama":
    return 1;
  endcase
  var conf := ReadConfigFile("::npcdesc");
  var elem;
  var points := 0;
  elem := FindConfigElem(conf, pettemplate);
  if (!elem)
    return 0;
  endif
  var difficulty := CInt(GetConfigInt(elem, "tameskill"));
  if (difficulty < 50)
    return 1;
  elseif ( ( difficulty - 20 ) > AP_GetSkill(player, TAMING ) )
    return 0;
  else
    return 1;
  endif
endfunction

function Lookiehere(who)
  var mobiles := ListMobilesNearLocation(who.x, who.y, who.z, 15, who.realm);
  var guard, num, i, hogcall, guardzone, element, range, timer, elem;
  var cfgfile := ReadConfigFile("::gzone");
  var entries := GetConfigStringKeys(cfgfile);
  foreach listing in entries
    element :=  cfgfile[listing];
    range := element.range;
    range := SplitWords(range);
    if((who.x >= CInt(range[1])) && (who.x <= CInt(range[3])) && (who.y >= CInt(range[2])) && (who.y <= CInt(range[4])))
      guardzone := 1;
      break;
    endif
  endforeach
  foreach npc in mobiles
	if(!npc.acctname)
	  elem := FindConfigElem(npccfg, npc.npctemplate);
	  if(!elem.guardignore && !GetObjProperty(npc, "called"));
		if((npc) && (npc.script != "tamed") && (npc.script != "light"))
          case(RandomInt(4) + 1)
            1: hogcall := "Guards! They're killing people here!";
            2: hogcall := "Guards! I pay my taxes and no guards? Guards!";
            3: hogcall := "Help! They're dying here! Guards!";
            4: hogcall := "Guards! Come save us quick!";
            5: hogcall := "Ah! Help us! Guards!";
          endcase
          PrintTextAbove(me, hogcall);
          if(guardzone == 1)
		    SetObjProperty(npc, "called", 1);
            num := RandomInt(4) + 1;
            foreach mobile in ListMobilesNearLocation(me.x, me.y, me.z, 15, me.realm)
		      if(mobile.npctemplate == "townguard")
		        var ev := array;
                ev.+ type;
                ev.+ source;
                ev.type := EVID_PEACEMADE;
                ev.source := npc;
                SendEvent(mobile, ev);
                ev.type := SYSEVENT_ENGAGED;
                SendEvent(mobile, ev);
                SetObjProperty(npc, "guardkill", 1);
              endif
            endforeach
          endif
		endif
	  endif
    else
	  timer := GetObjProperty(npc, "guardstimer");
	  if(npc.criminal && (timer < ReadGameClock() || timer.errortext))
        foreach mobile in ListMobilesNearLocation(me.x, me.y, me.z, 15, me.realm)
		  if(mobile.npctemplate == "townguard")
		    var ev := array;
            ev.+ type;
            ev.+ source;
            ev.type := EVID_PEACEMADE;
            ev.source := npc;
            SendEvent(mobile, ev);
            ev.type := SYSEVENT_ENGAGED;
            SendEvent(mobile, ev);
            SetObjProperty(npc, "guardkill", 1);
          endif
        endforeach
        SetObjProperty(npc, "guardstimer", ReadGameClock()+60);
	  endif
	endif
  endforeach
  sleep(1);
endfunction